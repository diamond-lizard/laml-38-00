; The LAML library and programs are written by Kurt Normark, Aalborg University, Denmark.
; Copyright (C) 2003 Kurt Normark, normark@cs.aau.dk.
; 
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
; 
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
; 
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

; .schemedoc-dependencies "../../../lib/xml-in-laml/man/xml-in-laml" "man/photo-show"


(lib-load "xml-in-laml/xml-in-laml.scm")

(laml-style "simple-xhtml1.0-transitional-validating")
(lib-load "xhtml1.0-convenience.scm")

(lib-load "color.scm")
(lib-load "time.scm")
(lib-load "file-read.scm")

(end-laml-loading)  ; it seems important to load .laml here (due to use of the system procedure).


(define (photo-show! ast)
  (photo-show-bridging! ast)
  (end-laml)
)

(define photo-show-dir (string-append laml-dir "styles/xml-in-laml/photo-show/"))
(load (string-append  photo-show-dir "mirror/laml-photo-mirror.scm"))

; ---------------------------------------------------------------------------------------------------

(define current-photo-ast #f)

;; A variable that controls if the tool can generate informative messages. 
;; A number where 0 means no messages, 1 means a minimal amount of messages, and 2 means many messages.
(define photo-show-verbosity-level 2)

;; The directory in which to write the output html files from this tool.
;; Defaults to (startup-directory). 
(define photo-destination-dir (startup-directory))

;; The name of the film in which the picture resides
(define film-identification 
  (if (parent-directory photo-destination-dir)
      (file-name-proper (substring photo-destination-dir 0 (- (string-length photo-destination-dir) 1)))
      "ROOT"))

;;; Default settings.
;;; The default settings apply as defaults for photo-entries.
;;; Individual photo entries overwrites the defaults. The defaults stem from
;;; the attributes and the contents of the root photo-show construct.

;; A default string for the upper captions of pictures (a string)
;; Assigned in photo-show-bridging!
(define default-upper-caption #f)

;; A default string for the upper font size of pictures (a number between 1 and 6)
;; Assigned in photo-show-bridging!
(define default-upper-size 0)

;; A default string for the lower captions of pictures (a string)
;; Assigned in photo-show-bridging!
(define default-lower-caption #f)

;; A default string for the lower font size of pictures (a number between 1 and 6)
;; Assigned in photo-show-bridging!
(define default-lower-size 0)

;; A default time (a number, in seconds). Show each picture default-time seconds
;; Assigned in photo-show-bridging!
(define default-time #f)

;; The default photo orientation. Used as the default in make-default-photo-entry.
;; Assigned in photo-show-bridging!
(define default-photo-orientation 'landscape)

;; The default photo size. Used as the default in make-default-photo-entry.
;; Assigned in photo-show-bridging!
(define default-photo-size 'auto)

;; The dimension of the photo overview index tables. An integer. 
;; Assigned in photo-show-bridging!
(define tabular-overview-dimension 3)


;; The width of photos in the visual index. Not used.
(define visual-index-photo-width 200)

;;; Other important variables.

;; The title of the show. 
;; Intended to be redefined after this tool is loaded. 
;; If not redefined, no title is shown.
(define photo-show-title #f)

; The URL prefix of the pictures. Never redefine this variable.
; The default works for pictures in the parent directory of the html directory.
(define picture-url-prefix "../")

;; The path to a designated photo home directory.
;; Relative to the directory hosting the photos.
(define home-path #f)

;; A URL which allows us to exit from the photo index. Like last-url, this URL must be relative.
;; It is interpreted as relative to the directory in which the photos reside.
;; This variable can be redefined after photo.scm (this tool) has been loaded.
;; If not redefined, no home url is presented.
(define home-url #f)

;; The default color of text.
(define default-photo-text-color #f)

;; The default background color.
(define default-photo-background-color #f)

;; The default transition - IE only.
(define default-transition #f)

;; Determines if raw or pretty printed html is being generated by this tool.
;; A symbol.
(define photo-show-html-mode 'raw)

;; Determines if index pages links to stopped or running photo pages.
(define photo-show-index-linking 'stop)

; Software constant. Not settable from laml photo file.
(define default-menu-font-size-string "10pt")

; The width of the photo table window. Used to determine width of n x n landscape photos in an overview overview.
; Assigned later
(define photo-table-window-width #f) ; 970

; The height of the photo table window. Used to determine the height of n x n portrait photos in an overview presentation.
; Assigned later
(define photo-table-window-height #f)


; The width in percentages for auto landscape photos
(define default-landscape-percentage 95)
(define landscape-percentage default-landscape-percentage)

; The height in percentages for auto portait photos
(define default-portrait-percentage 90)
(define portrait-percentage default-portrait-percentage)

; The number of invisible left margin pixels of a photo. Displaces the photo to the right. A number of pixels
; Used to compensate for invisible scroll bar.
(define default-photo-displacement-left 0)
(define photo-displacement-left default-photo-displacement-left)

; The number of invisible right margin pixels of a photo. Displaces the photo to the left. A number of pixels
(define default-photo-displacement-right 0)
(define photo-displacement-right default-photo-displacement-right)

; The name of the camera used. This allows special handling of the photos from different camaras.
; Currently, the rotation suffix is Canon G3 specific.
(define camera-used #f)

; Canon G3 relevant. Also used as suffix by Imagemagick rotation. 
(define rotated-suffix-name "_r1")

(define clipboard-support? #t)  ; An IE. facility to copy LAML photo clause via the 'Q' or 'q' key.

(define photo-show-credit "")

;;; Contextual photo show state.

(define photo-show-previous-film #f)
(define photo-show-next-film #f)
(define photo-show-before-first #f)
(define photo-show-after-last #f)

; Time stamp related variables. Assigned later.
(define time-stamp #f)
(define time-stamp-presentation #f)

; ---------------------------------------------------------------------------------------------------
; CGI program variables - default initialization of

(define photo-dir-name (file-name-proper (startup-directory)))  ; MzScheme specific. Earlier (current-directory)
; Gives "" - remove trailing '/' before using file-name-proper, or find a better way.

; ---------------------------------------------------------------------------------------------------
; Photo ast selectors

(define (photo-file-name-of photo-ast)
  (get-prop 'file (ast-attributes photo-ast)))

; Return the path of photo-ast, of #f if no path is given
(define (photo-path-of photo-ast)
  (defaulted-get-prop 'path (ast-attributes photo-ast) #f))

(define (photo-show-time-of photo-ast)
  (let ((time (defaulted-get-prop 'time (ast-attributes photo-ast) #f)))
    (if time time default-time)))

(define (photo-upper-caption-of photo-ast)
 (let ((upper-caption-ast (ast-subtree photo-ast "upper-caption")))
  (if upper-caption-ast
      (let ((caption (aggregated-ast-cdata-contents upper-caption-ast)))
        (if (blank-string? caption)
            default-upper-caption
            caption))
      default-upper-caption)))

(define (photo-lower-caption-of photo-ast)
 (let ((lower-caption-ast (ast-subtree photo-ast "lower-caption")))
  (if lower-caption-ast
      (let ((caption (aggregated-ast-cdata-contents lower-caption-ast)))
        (if (blank-string? caption)
            default-lower-caption
            caption))
      default-lower-caption)))

(define (photo-upper-size-of photo-ast)
 (let ((upper-caption-ast (ast-subtree photo-ast "upper-caption")))
  (if upper-caption-ast
      (let ((size (as-symbol (defaulted-get-prop 'size (ast-attributes upper-caption-ast) "auto"))))
        (if size 
            size
            default-upper-size))
      default-upper-size)))

(define (photo-lower-size-of photo-ast)
 (let ((lower-caption-ast (ast-subtree photo-ast "lower-caption")))
  (if lower-caption-ast
      (let ((size (as-number-or-false (defaulted-get-prop 'size (ast-attributes lower-caption-ast) #f))))
        (if size 
            size
            default-lower-size))
      default-lower-size)))

; Returns the actual given photo height, or #f if no such height is supplied in the photo-ast
(define (photo-height-of photo-ast)
  (let ((height (as-number-or-false (defaulted-get-prop 'height (ast-attributes photo-ast) #f))))
    (if height
        height
        #f)))

; Returns the actual given photo width, or #f if no such width is supplied in the photo-ast
(define (photo-width-of photo-ast)
  (let ((width (as-number-or-false (defaulted-get-prop 'width (ast-attributes photo-ast) #f))))
    (if width
        width
        #f)))

(define (photo-orientation-of photo-ast)
  (let ((orientation (as-symbol (defaulted-get-prop 'orientation (ast-attributes photo-ast) "landscape"))))
    (if orientation
        orientation
        default-photo-orientation)))

(define (photo-size-of photo-ast)
  (let ((size (as-symbol (defaulted-get-prop 'size (ast-attributes photo-ast) "auto"))))
    (if size
        size
        default-photo-size)))

; ---------------------------------------------------------------------------------------------------
; Page name functions.

; Return the name of the n'th tabular overview page of dimension dim. 
; No path, and no extension is returned.
(define (tabular-page-name dim n)
  (string-append "index" "-" (as-string dim) "-" (as-string n)))

(define (help-file-name) 
  (if (eq? language-preference 'danish) "help-dk" "help-en"))

(define (about-file-name) 
  (if (eq? language-preference 'danish) "about-dk" "about-en"))


; ---------------------------------------------------------------------------------------------------


(define (make-default-photo-entry photo-file)
  (photo 
    'time (as-string default-time)
    'file photo-file
    'path ""
    'size (as-string default-photo-size)
    'orientation (if (rotated-photo-name? photo-file) "portrait" (as-string default-photo-orientation))
    (upper-caption default-upper-caption 'size (as-string default-upper-size))
    (lower-caption default-lower-caption 'size (as-string default-lower-size))))

(define (rotated-photo-name? photo-file-name)
  (if (and camera-used (equal? camera-used "Canon-G3"))
      (let* ((proper-name (file-name-proper photo-file-name))
             (lgt (string-length proper-name))
             (suffix (if (>= lgt 3)
                         (substring proper-name (- lgt 3) lgt)
                         #f)))
        (and suffix (equal? suffix rotated-suffix-name)))
      #f))
       


; this-photo-entry is a photo AST.
; next-photo-entry and prev-photo-entry may be ASTs, or #f if they do not make sense relative to this-photo-entry
(define (make-single-photo-page this-photo-entry next-photo-entry prev-photo-entry n)
 (let ((this-file-name (photo-file-name-of this-photo-entry))
       (this-path (photo-path-of this-photo-entry))
       (this-show-time (photo-show-time-of this-photo-entry))
       (this-upper-caption (photo-upper-caption-of this-photo-entry))
       (this-upper-size (photo-upper-size-of this-photo-entry))
       (this-lower-caption (photo-lower-caption-of this-photo-entry))
       (this-lower-size (photo-lower-size-of this-photo-entry))
       (given-orientation (photo-orientation-of this-photo-entry))
       (given-size (photo-size-of this-photo-entry))
       (given-photo-height (photo-height-of this-photo-entry))
       (given-photo-width (photo-width-of this-photo-entry))
       (next-file-url (if next-photo-entry (picture-name-to-url (photo-file-name-of next-photo-entry)) (next-url-of-this-show))) 
       (prev-file-url (if prev-photo-entry (picture-name-to-url (photo-file-name-of prev-photo-entry)) (previous-url-of-this-show)))
      )

  (if (not (file-exists? (string-append photo-destination-dir (if this-path this-path "") this-file-name)))
      (display-warning 
         (text-choice "Reference til ikke eksisterende foto:" "Reference to non-existing photo:") 
         (string-append (if this-path this-path "") this-file-name)))

  (if (>= photo-show-verbosity-level 2) (display-message this-file-name))

  (write-timed-photo-page
    this-path this-file-name this-show-time next-file-url prev-file-url this-upper-caption this-upper-size this-lower-caption this-lower-size n
    given-size given-orientation given-photo-width given-photo-height)
  (write-stopped-photo-page
    this-path this-file-name this-show-time next-file-url prev-file-url this-upper-caption this-upper-size this-lower-caption this-lower-size n
    given-size given-orientation given-photo-width given-photo-height)
 )
)


(define (next-url-of-this-show)
  (cond ((eq? photo-show-after-last 'none) "")                                                                      ; No link to next - just ends

        ((eq? photo-show-after-last 'stop-page) "end-page.html")                                                    ; End page

        ((eq? photo-show-after-last 'first-picture)                                                                 ; Cyclic
            (first-url-of-photo-ast current-photo-ast ""))

        ((and photo-show-next-film (directory-exists? (string-append photo-destination-dir photo-show-next-film))   ; First picture in next film
              (eq? photo-show-after-last 'next-film))                              
            (first-url-of-photo-ast (get-photo-ast photo-show-next-film) photo-show-next-film ))

        (else (begin
                (if (> photo-show-verbosity-level 0)
                    (display-message "Problems determining final url of show. Linking to End Page."))
                "end-page.html"))))


(define (previous-url-of-this-show)
  (cond ((eq? photo-show-before-first 'none) "")

        ((eq? photo-show-before-first 'last-picture) 
            (last-url-of-photo-ast current-photo-ast ""))

        ((and photo-show-previous-film (directory-exists? (string-append photo-destination-dir photo-show-previous-film))
              (eq? photo-show-before-first 'previous-film))
            (last-url-of-photo-ast (get-photo-ast photo-show-previous-film) photo-show-previous-film))

        (else (begin
                (if (> photo-show-verbosity-level 0)
                    (display-message "Problems determining first url of show. No previous link from first page."))
                ""))))


; Generate a html url for picture-name.
; If the optional parameter stop? is passed and non-#f, a stop url is returned.
; .form (picture-name-to-url picture-name [stop?])
(define (picture-name-to-url picture-name . optional-parameter-list)
 (let ((stop? (optional-parameter 1 optional-parameter-list #f)))
  (string-append (file-name-proper picture-name)
                 (if stop? "-stop" "")
                  "." "html")))

(define (meta-tag-clauses)
   (list
     (list 'name "Generator" 'content "LAML")
   )
)


; Write a photo on a html page and provide for a continuation to next-picture.
; This-picture is a jpg or gif file names with extensions, but without initial path.
; Next-picture-url is an URL of the next picture (html extension).
; The remaining parameters are captions and their font sizes.
(define (write-timed-photo-page this-path this-picture show-time next-picture-url prev-picture-url
                                upper-caption upper-size lower-caption lower-size n size orientation actual-width actual-height) 
 (let* ((this-picture-name (file-name-proper this-picture))
        (next-picture-name (file-name-proper next-picture-url))
        (this-picture-stop-url (string-append this-picture-name "-stop.html"))
        (colors-list (list default-photo-background-color default-photo-text-color default-photo-text-color default-photo-text-color))
        (bg-color (first colors-list))
        (text-color (second colors-list))
        (link-color (third colors-list))
        (vlink-color (fourth colors-list)))
  (write-html (list photo-show-html-mode 'prolog)
   (html 
    (head 
     (title (text-choice "LAML foto fremvisning" "LAML photo show"))

     (cond ((equal? default-transition "blend")
              (meta 'http-equiv "Page-Enter" 'content "blendTrans(Duration=5.0)"))
           (else 
              (meta 'http-equiv "Page-Enter" 'content "blendTrans(Duration=0.0)")))

     (meta 'name "Generator" 'content "LAML")

     ; Time out script:     
     (script "function navigateNext(nextUrl,ms){setTimeout(\"window.location = \" + \"'\" + nextUrl + \"'\",ms);}" 
             'type "text/javascript")

     ; Key control script:
     (script "" 'src "javascript/photoNavigate.js" 'type "text/javascript")

     (link 'href "stylesheets/photo-style.css"  'rel "stylesheet" 'title "photo-style" 'type "text/css")
 
    )
    (body
      'onload 
      (string-append 
       (js-call "photoNavigateRunning" 
                     (map string-it-single 
                           (list this-picture-stop-url))
           ) "; " 
       (js-call "navigateNext" (list (string-it-single next-picture-url) (* (as-number show-time) 1000))))

;      'scroll "no"

      'bgcolor bg-color
      'text text-color
      'link link-color
      'vlink vlink-color

      (show-picture this-path this-picture upper-caption upper-size lower-caption lower-size n photo-list-length #t 
                        next-picture-url #f #f  size orientation actual-width actual-height)))

    (string-append photo-destination-dir "html/" this-picture-name ".html"))))


; A variant of write-timed-photo-page, which does not apply automatic time out.
(define (write-stopped-photo-page this-path this-picture show-time next-picture-url prev-picture-url
                                 upper-caption upper-size lower-caption lower-size n size orientation actual-width actual-height) 
 (let* ((this-picture-name (file-name-proper this-picture))
        (next-picture-name (file-name-proper next-picture-url))
        (next-picture-init-path (file-name-initial-path next-picture-url))
        (next-picture-stop-url (if (empty-string? next-picture-url) 
                                   "" 
                                   (string-append next-picture-init-path next-picture-name "-stop" ".html")))
        (prev-picture-name (file-name-proper prev-picture-url))
        (prev-picture-init-path (file-name-initial-path prev-picture-url))
        (prev-picture-stop-url (if (empty-string? prev-picture-url) 
                                   ""
                                   (string-append prev-picture-init-path prev-picture-name "-stop" ".html")))
        (run-url next-picture-url)
        (colors-list (list default-photo-background-color default-photo-text-color default-photo-text-color default-photo-text-color))
        (bg-color (first colors-list))
        (text-color (second colors-list))
        (link-color (third colors-list))
        (vlink-color (fourth colors-list))
       )
  (write-html (list photo-show-html-mode 'prolog)
   (html 
    (head 
     (title (text-choice "LAML foto fremvisning" "LAML photo show"))
     (script "" 'src (string-append "javascript/photoNavigate.js") 'type "text/javascript")
     (link 'href "stylesheets/photo-style.css"  'rel "stylesheet" 'title "photo-style" 'type "text/css")
     (meta 'name "Generator" 'content "LAML")

    )
    (body 
      'onload (js-call "photoNavigate" 
                  (append
                    (map string-it-single 
                          (list next-picture-stop-url
                                prev-picture-stop-url
                                run-url
                                (tabular-url "" tabular-overview-dimension (+ 1 (quotient (- n 1) (* tabular-overview-dimension tabular-overview-dimension))))
                                "../index.html"  ; x
                                "" ""
                                ))
                    (list (js-string-array '())))
              )
      'bgcolor bg-color
      'text text-color
      'link link-color
      'vlink vlink-color
      (show-picture this-path this-picture upper-caption upper-size lower-caption lower-size n photo-list-length #f 
                        next-picture-url next-picture-stop-url prev-picture-stop-url size orientation actual-width actual-height)
      


    )
   )
   (string-append photo-destination-dir "html/" this-picture-name "-stop" ".html"))))


; Show the page contents in term of a picture. The picture, captions and font sizes are given as parameters.
; picture-path is #f if not path is given; It means that picture is in .. relatative to the html directory.
; if picture-path is non-#f, the picuture is found in ../picture-path relative to the html directory.
; If stop? is true, a stop link is provided for. If not stop?, a run link bringing us to next-url is presented.
; next-stop-url and prev-stop-url are only used is stop? is false.
(define (show-picture picture-path picture-file-name upper-caption upper-size lower-caption lower-size n total-number 
                      stop? next-url next-stop-url prev-stop-url size orientation actual-width actual-height)
 (let ((hs-space (horizontal-space 1))
       (real-picture-url-prefix
         (string-append picture-url-prefix 
                        (if picture-path picture-path "")))
       (as-string-percent (lambda (x) (string-append (as-string x) "%")))
       (css-margin-settings 
          (list 'css:margin-left  (string-append (as-string photo-displacement-left) "px")
                'css:margin-right (string-append (as-string photo-displacement-right) "px")))
      )
  (center 
    
     (left-middle-right-banner 
        (con 
          (a (text-choice "Indhold" "Index") 'href "../index.html" 'class "menu-item" (tool-tip "x")) hs-space
          (tabular-link "" (+ 1 (quotient (- n 1) (* tabular-overview-dimension tabular-overview-dimension))) "none" "u")  hs-space
          (if stop?
             (a "Stop" 'href (string-append (file-name-proper picture-file-name) "-stop.html") 'class "menu-item")
             (con (a (text-choice "Kør" "Run") 'href next-url 'class "menu-item" (tool-tip "r") )  hs-space
                  (if (not (empty-string? prev-stop-url))
                      (a (text-choice "Forrige" "Previous") 'href prev-stop-url 'class "menu-item" (tool-tip "p"))
                      (text-choice "Forrige" "Previous"))  hs-space
                  (if (not (empty-string? next-stop-url))
                      (a (text-choice "Næste" "Next") 'href next-stop-url 'class "menu-item" (tool-tip "n")) 
                      (text-choice "Næste" "Next"))
             )
          )
        )
 
        (let* ((this-file-name-proper (file-name-proper picture-file-name))
               (this-file-time-stamp (get-jpg-file-time-stamp (string-append (in-startup-directory (if picture-path picture-path "")) picture-file-name)))
               (this-file-time-stamp-string
                 (if this-file-time-stamp
                     (let* ((dt (date-time this-file-time-stamp))
                            (d  (car dt))
                            (t  (cadr dt))
                            (wd (capitalize-string-nd (weekday this-file-time-stamp)))
                           )
                       (cond ((eq? time-stamp-presentation 'date-time)
                                 (string-append d ", " t))
                             ((eq? time-stamp-presentation 'date)
                                 d)
                             ((eq? time-stamp-presentation 'time)
                                 t)
                             ((eq? time-stamp-presentation 'weekday-date)
                                 (string-append wd " - " d))
                             ((eq? time-stamp-presentation 'weekday-date-time)
                                 (string-append wd " - " d ", " t))
                             ((eq? time-stamp-presentation 'none)
                                 "")))
                      ""))
              )
         (span 'class "menu-item"  
               film-identification
               hs-space "-" hs-space 
               (a 'href (string-append "laml-fragments.html" "#" this-file-name-proper )
                  'target "fragments"
                  (as-string n) " : " (as-string total-number) 'css:text-decoration "none") 
               hs-space "-" hs-space 
               (a 'href (string-append real-picture-url-prefix picture-file-name) (font 'size "2" this-file-name-proper) 'css:text-decoration "none")
               (if (and picture-path (not (empty-string? picture-path)))
                   (list 
                     hs-space "-" hs-space 
                     (a 'href (string-append "../" picture-path "html/" this-file-name-proper "-stop" "." "html")
                              (text-choice "Original" "Original") 'css:text-decoration "none")
                   )
                   '())
               (if (not (empty-string? this-file-time-stamp-string))
                   (list
                     hs-space "-" hs-space 
                     this-file-time-stamp-string)
                   '())
          )
        )

        (con
         (if home-url
            (a (text-choice "Hjem" "Home") 'href (string-append "../" home-url) 'class "menu-item")
            "") hs-space
         (a (text-choice "Om" "About") 'href (string-append (about-file-name) ".html") 'class "menu-item") hs-space
         (a (text-choice "Hjælp" "Help") 'href (string-append (help-file-name) ".html") 'class "menu-item")
        )
        '(20 60 20)
     )

     (if upper-caption 
         (list (font 'size (as-string upper-size) 'color default-photo-text-color (b upper-caption)) (p))
         (p))

  
     (cond ((and (eq? size 'original))  
              (img css-margin-settings 'src (string-append real-picture-url-prefix picture-file-name) 'alt ""))

           ((and (eq? size 'auto) (eq? orientation 'landscape) (not actual-width) (not actual-height))  
              (img css-margin-settings 'width (as-string-percent landscape-percentage)  'src (string-append real-picture-url-prefix picture-file-name) 'alt ""))

           ((and (eq? size 'auto) (eq? orientation 'portrait) (not actual-width) (not actual-height))
              (img css-margin-settings 'height (as-string-percent portrait-percentage) 'src (string-append real-picture-url-prefix picture-file-name) 'alt ""))

           ((and (eq? size 'explicit) actual-height actual-width)
              (img css-margin-settings 'height (as-string actual-height) 'width (as-string actual-width) 
                         'src (string-append real-picture-url-prefix picture-file-name) 'alt ""))
           ((and (eq? size 'explicit) actual-height)
              (img css-margin-settings 'height (as-string actual-height) 'src (string-append real-picture-url-prefix picture-file-name) 'alt ""))
           ((and actual-width (eq? size 'explicit))
              (img css-margin-settings 'width (as-string actual-width) 'src (string-append real-picture-url-prefix picture-file-name) 'alt ""))
           (else (img css-margin-settings 'src (string-append real-picture-url-prefix picture-file-name) 'alt "") ))

     (p)

     (if lower-caption 
         (list (font 'size (as-string lower-size) 'color default-photo-text-color (b lower-caption)) (p))
         "")

     (if clipboard-support?     ; stuff that facilitates copying to clipboard via javascript function. 
                                ; Taken from http://www.htmlgoodies.com/beyond/clipboard.html
                                ; Activated via 'Q' or 'q' key.
         (list
            (span 'id "copytext"  (laml-single-photo-fragment-proper picture-file-name) 'style "display: none;" )
            (textarea 'id "holdtext" 'rows "1" 'cols "10" 'style "display: none;"))
         "")

)))






;;; Top level functions.
;;; The functions in this section are used in the LAML script which generates the photo show.

;; Make the photo-show from photo-list in terms of individual HTML files and an index file.
;; Photo-list is a list of photo-ASTs.
(define (make-photo-show photo-list previous-film-path next-film-path)
 (let ((html-dir (string-append photo-destination-dir "html"))
       (internal-dir (string-append photo-destination-dir "internal"))
      )

  (make-photo-index photo-list previous-film-path next-film-path)


  (map4
    make-single-photo-page
    photo-list
    (if (not (null? photo-list)) (append (cdr photo-list) (list #f)) '())
    (if (not (null? photo-list)) (append (list #f) (butlast photo-list)) '())
    (number-interval 1 (length photo-list))
  )

  ; write the first.lsp file
  ; (if (not (null? photo-list))
  ;  (file-write
  ;   (file-name-proper (photo-file-name-of (first photo-list)))
  ;   (string-append (startup-directory) "first.lsp")))

  (ensure-directory-existence! (string-append photo-destination-dir "html/") "javascript")
  (copy-text-file (string-append photo-show-dir "javascript/" "photoNavigate.js")
                  (string-append photo-destination-dir "html/" "javascript/photoNavigate.js")
                  #t)                  ; overwrite if necessary

  (ensure-directory-existence! (string-append photo-destination-dir "html/") "stylesheets")

  (let* ((style-sheet-string (read-text-file (string-append photo-show-dir "stylesheets/" "photo-style.css")))
         (modified-style-sheet-string
           (replace-string
            (replace-string style-sheet-string "@background-color@" default-photo-background-color)
            "@menu-font-size@"
            default-menu-font-size-string))
         (target-file-path (string-append photo-destination-dir "html/" "stylesheets/photo-style.css"))
        )
    (if (file-exists? target-file-path) (delete-file target-file-path))
    (write-text-file modified-style-sheet-string target-file-path))

  ;(copy-text-file (string-append photo-show-dir "stylesheets/" "photo-style.css")
  ;                (string-append photo-destination-dir "html/" "stylesheets/photo-style.css")
  ;                #t)

  (if (>= photo-show-verbosity-level 1) 
      (display-message (string-append (as-string (length photo-list)) " " "photos pages generated in " film-identification)))
  (if (>= photo-show-verbosity-level 1)
      (display-message "DONE. Bring  index.html  up in you browser to start the show."))))

(define photo-list-length 0) ; assigned in make-photo-show

(define (photo-page-body-colors)
  (list
    'bgcolor default-photo-background-color 'text default-photo-text-color
    'link default-photo-text-color 'vlink default-photo-text-color))



; Make the photo index file. The parameter photo-list is a list of photo ASTs.
(define (make-photo-index photo-list previous-film-path next-film-path)
 (let* ((hs-space (horizontal-space 3))
        (overview-count (tabular-page-count tabular-overview-dimension (length photo-list)))
        (down-links (map (lambda (n) (string-append "html/" "index" "-" (as-string tabular-overview-dimension) "-" (as-string n) "." "html"))
                         (number-interval 1 overview-count)))
        (first-photo-url (if (not (null? photo-list))
                             (string-append "html/" (picture-name-to-url (photo-file-name-of (car photo-list)) "stop"))
                             ""))
        (last-photo-url (if (not (null? photo-list))
                             (string-append "html/" (picture-name-to-url (photo-file-name-of (last photo-list)) "stop"))
                             ""))
       )
  (write-html (list photo-show-html-mode 'prolog)
  (html
   (head (title (text-choice "Fotooversigt" "Photo index"))
         (script "" 'src (string-append "html/javascript/photoNavigate.js") 'type "text/javascript")
;         (link 'href "html/stylesheets/photo-style.css"  'rel "stylesheet" 'title "photo-style" 'type "text/css")
         (meta 'name "Generator" 'content "LAML")
   )
   (body  
    'onload (js-call "photoNavigate" 
                (append
                  (map string-it-single 
                          (list (if (and next-film-path (directory-exists? (string-append photo-destination-dir next-film-path)))
                                    (string-append next-film-path "index.html")
                                    "")
                                (if (and previous-film-path (directory-exists? (string-append photo-destination-dir previous-film-path)))
                                    (string-append previous-film-path "index.html")
                                    "")
                                ""
                                (if home-url home-url "")
                                first-photo-url  ; x
                                last-photo-url ; y
                                "" ; z
                                ))
                   (list (js-string-array down-links))
                )
             )

    (photo-page-body-colors)
    (left-middle-right-banner 
          (con 
            (if (and previous-film-path (directory-exists? (string-append photo-destination-dir previous-film-path)))
                (a (text-choice "Forrige film" "Previous film") 'href (string-append previous-film-path "index.html") 'class "menu-item" (tool-tip "p"))
                (text-choice "Forrige film" "Previous film"))  hs-space
            (if (and next-film-path (directory-exists? (string-append photo-destination-dir next-film-path)))
                (a (text-choice "Næste film" "Next film") 'href (string-append next-film-path "index.html") 'class "menu-item" (tool-tip "n"))
                (text-choice "Næste film" "Next film"))

          )
          (em film-identification)
          (con
           (if home-url
               (a (text-choice "Hjem" "Home") 'href home-url  (tool-tip "u"))
               "") hs-space
           (a (text-choice "Om" "About") 'href (string-append "html/" (about-file-name) ".html") ) hs-space
           (a (text-choice "Hjælp" "Help") 'href (string-append "html/" (help-file-name) ".html") ))
    )
    (h1 (if photo-show-title photo-show-title (text-choice "Fotooversigt" "Photo Index")))
    (if (not (null? photo-list))
        (table 'border "0"
               (map2 photo-index-entry photo-list (number-interval 1 (length photo-list))))
        (em (text-choice "Ingen billeder" "No pictures")))

    (p)
;    (if last-url (a-tag last-url (text-choice "Fortsættelse" "Continuation of show")) "")
    (vertical-space 2)
    (font-size "2" photo-show-credit) (br)
    (font 'size "2" (when-generated))
   ))
   (string-append photo-destination-dir "index.html"))))

; Make an entry in the photo index
(define (photo-index-entry photo-entry n)
 (let ((caption (photo-lower-caption-of photo-entry))
       (timing (photo-show-time-of photo-entry)))
  (tr (td 'width "50" (as-string n) _ "." 'align "right" (horizontal-space 2))
      (td 'width "300" 
           (a 'href
             (string-append "html/" 
                            (cond ((eq? photo-show-index-linking 'stop) (picture-name-to-url (photo-file-name-of photo-entry) "stop"))
                                  ((eq? photo-show-index-linking 'run) (picture-name-to-url (photo-file-name-of photo-entry)))
                                  (else (laml-error "photo-index-entry:" "Unknown photo-show-index-linking" photo-show-index-linking))))
             (if caption caption (con (text-choice "Fotonummer" "Photo number") (horizontal-space 1) (as-string n)))
             ))
      (td 'width "50"
          (if (= 0 (remainder (- n 1) (* tabular-overview-dimension tabular-overview-dimension)))
              (tabular-link "html/" (+ 1 (quotient (- n 1) (* tabular-overview-dimension tabular-overview-dimension))) "underline")
              (horizontal-space 1)))
      (td 'width "200" (font-size 2 (photo-file-name-of photo-entry)))
      (td 'width "100" (font-size 2 (con (as-string timing) " " (text-choice "sekunder" "seconds"))))
  )))

; Return the number of tabular overview pages of dimension dim in a collection of photo-numbers photos.
(define (tabular-page-count dim photo-numbers)
  (+ (quotient (- photo-numbers 1) (* dim dim)) 1))

(define (tabular-url init-url-path dim i)
 (let ((url (string-append (tabular-page-name dim i) "." "html"))
      )
  (string-append init-url-path url)))

(define (tabular-link init-url-path i . optional-parameter-list)
 (let ((text-decoration (optional-parameter 1 optional-parameter-list "none"))
       (short-cut-letter (optional-parameter 2 optional-parameter-list #f))
       (times (string-append (as-string tabular-overview-dimension) " x " (as-string tabular-overview-dimension)))
      )
  (a 'href (tabular-url init-url-path tabular-overview-dimension i)  times 'css:text-decoration text-decoration (tool-tip short-cut-letter))))



; ---------------------------------------------------------------------------------------------------

; Take one level off relative-file-name. A generally useful function.
(define (take-one-level-from relative-file-name)
 (let ((forward-slash-pos (find-in-string relative-file-name #\/))
       (backward-slash-pos (find-in-string relative-file-name #\\))
       (lgt (string-length relative-file-name))
      )
   (cond ((and forward-slash-pos backward-slash-pos (<= forward-slash-pos backward-slash-pos))
             (substring relative-file-name (+ 1 forward-slash-pos) lgt))
         ((and forward-slash-pos backward-slash-pos (<= backward-slash-pos forward-slash-pos))
             (substring relative-file-name (+ 1 backward-slash-pos) lgt))
         (forward-slash-pos
             (substring relative-file-name (+ 1 forward-slash-pos) lgt))
         (backward-slash-pos
             (substring relative-file-name (+ 1 backward-slash-pos) lgt))
         (else #f))))



(define (photo-filename-leq? fn1 fn2)
  (string<=? (downcase-string fn1) (downcase-string fn1)))
  
;; Return a list of photo-ASTs sorted alphabetically, in the LAML startup directory.
;; A photo entry is a list of the form ("photo-file.ext" show-time "upper-caption" upper-size "lower-caption" lower-size)
;; upper-caption and/or lower-caption may be #f in case no caption apply; In that case upper-size and lower-size (integers, seconds)
;; do not matter.
(define (photo-asts-in-current-directory)
  (let* ((dir-list (directory-list (startup-directory)))
         (picture-list
           (filter 
             (lambda (fn) (or (equal? "jpg" (file-name-extension fn)) (equal? "gif" (file-name-extension fn))
                              (equal? "JPG" (file-name-extension fn)) (equal? "GIF" (file-name-extension fn))))
             dir-list))
         (sorted-picture-list (sort-list picture-list photo-filename-leq?))
        )
    (map make-default-photo-entry picture-list)))



(define (end-page)
 (let ((the-first-url (first-url-of-photo-ast current-photo-ast #f "-stop"))
       (the-last-url (last-url-of-photo-ast current-photo-ast #f "-stop")))
  (html
   (head (title (text-choice "Slut side" "End page"))
         (script "" 'src (string-append "javascript/photoNavigate.js") 'type "text/javascript")
         (link 'href "stylesheets/photo-style.css"  'rel "stylesheet" 'title "photo-style" 'type "text/css")
         (meta 'name "Generator" 'content "LAML")
   )
   (body
    'onload (js-call "photoNavigate" 
                (append
                  (map string-it-single 
                          (list "" the-last-url ""
                                "../index.html"
                                "" "" ""
                                ))
                  (list (js-string-array (list the-first-url)))
                )
             )

    (photo-page-body-colors)
    (center
       (vertical-space 4)
       (font 'size "6" (b (text-choice "Slut" "The End"))) (br)
       (vertical-space 2)

       (a 'href the-last-url (font-size '2 (text-choice "Sidste foto" "Last photo"))
                'css:text-decoration "none") (br)
       (a 'href the-first-url (font-size '2 (text-choice "Første foto" "First photo"))
                'css:text-decoration "none") (p)

       (a 'href "../index.html" (font-size '2 (text-choice "Indhold" "Index")) 'css:text-decoration "none")  (br)
       (if home-url 
           (a 'href (string-append "../" home-url) (font-size 2 (text-choice "Hjem" "Home")) 'css:text-decoration "none")
           "")

    )))))

;; Return a sublist of photo-input-list, namely the photo entries in number-list.
;; The first photo is number 1.
(define (take-subset-by-numbers number-list photo-input-list)
   (if (null? number-list)
       '()
       (let ((actual-photo (get-photo-number (car number-list) photo-input-list)))
         (cons actual-photo (take-subset-by-numbers (cdr number-list) photo-input-list)))))
 
 (define (get-photo-number i phlst)
   (list-ref phlst (- i 1)))




; ------------------------------------------------------------------------------------------------------------------------

(define as-number-or-false
  (lambda (x)
    (cond ((or (string? x) (number? x)) (as-number x))
          ((and (boolean? x) (not x)) #f)
          (else (laml-error "as-number-or-false: expects number string or #f" x)))))

(define (photo-show-bridging! photo-show-ast)
 (let ((html-dir (string-append photo-destination-dir "html/"))
       (internal-dir (string-append photo-destination-dir "internal/"))
      )
  (display-message 
   (string-append 
     "The XML-in-LAML photo show generator, LAML " laml-version "."
   ))

  (if (>= photo-show-verbosity-level 1) (display-message "Processing " film-identification))

  (if (not (directory-exists? html-dir))
      (make-directory-in-directory photo-destination-dir "html/"))
  (if (not (directory-exists? internal-dir))
      (make-directory-in-directory photo-destination-dir "internal/"))
 
  (let* (
 
         (title-of-photo-show (aggregated-ast-cdata-contents (ast-subtree photo-show-ast "title-of-show")))
         (upper-caption-ast (ast-subtree photo-show-ast "upper-caption"))
         (lower-caption-ast (ast-subtree photo-show-ast "lower-caption"))
         (photo-show-upper-caption (aggregated-ast-cdata-contents upper-caption-ast))
         (photo-show-lower-caption (aggregated-ast-cdata-contents lower-caption-ast))
         (photo-show-upper-size (as-number (defaulted-get-prop 'size (ast-attributes upper-caption-ast) 3)))
         (photo-show-lower-size (as-number (defaulted-get-prop 'size (ast-attributes lower-caption-ast) 3)))
 
         (photo-list-ast (ast-subtree photo-show-ast "photo-list"))
         (photos-in-current-directory-ast (ast-subtree photo-show-ast "photos-in-current-directory"))
 
         (time-attr (as-number (get-prop 'time (ast-attributes photo-show-ast))))
         (background-color-attr (defaulted-get-prop 'background-color (ast-attributes photo-show-ast) (rgb-color-encoding 'black)))
         (text-color-attr (defaulted-get-prop 'text-color (ast-attributes photo-show-ast) (rgb-color-encoding 'white)))
         (home-path-attr (defaulted-get-prop 'home-path (ast-attributes photo-show-ast) #f))
         (photo-height-attr (as-number-or-false (defaulted-get-prop 'default-height (ast-attributes photo-show-ast) #f)))
         (photo-width-attr (as-number-or-false (defaulted-get-prop 'default-width (ast-attributes photo-show-ast) #f)))

         (previous-film-attr (defaulted-get-prop 'previous-film (ast-attributes photo-show-ast) #f))
         (next-film-attr (defaulted-get-prop 'next-film (ast-attributes photo-show-ast) #f))
         (before-first-attr (as-symbol (defaulted-get-prop 'before-first (ast-attributes photo-show-ast) "last-picture")))
         (after-last-attr (as-symbol (defaulted-get-prop 'after-last (ast-attributes photo-show-ast) "first-picture")))

         (transition-attr (defaulted-get-prop 'transition (ast-attributes photo-show-ast) "none"))
         (verbosity-level-attr (as-number (defaulted-get-prop 'verbosity-level (ast-attributes photo-show-ast) 2)))
         (langauge-attr (defaulted-get-prop 'language (ast-attributes photo-show-ast) "english"))
         (html-mode-attr (as-symbol (defaulted-get-prop 'html-mode (ast-attributes photo-show-ast) "raw")))
         (index-linking-attr (as-symbol (defaulted-get-prop 'index-linking (ast-attributes photo-show-ast) "stop")))
         (default-size-attr (as-symbol (defaulted-get-prop 'default-size (ast-attributes photo-show-ast) "auto")))
         (default-orientation-attr (as-symbol (defaulted-get-prop 'default-orientation (ast-attributes photo-show-ast) "landscape")))
         (tabular-overview-dimension-attr (as-number (defaulted-get-prop 'tabular-overview-dimension (ast-attributes photo-show-ast) "3")))
         (photo-tabular-window-width-attr (as-number (defaulted-get-prop 'photo-tabular-window-width (ast-attributes photo-show-ast) "870")))
         (photo-tabular-window-height-attr (as-number (defaulted-get-prop 'photo-tabular-window-height (ast-attributes photo-show-ast) "740")))

         (copy-film-attr (as-boolean (defaulted-get-prop 'copy-film (ast-attributes photo-show-ast) "false")))
         (copy-destination-attr (defaulted-get-prop 'copy-destination (ast-attributes photo-show-ast) "c:/temp/"))

         (shave-resize-and-copy-attr (as-boolean (defaulted-get-prop 'shave-resize-and-copy (ast-attributes photo-show-ast) "false")))         
         (shave-width-attr (as-number (defaulted-get-prop 'shave-width (ast-attributes photo-show-ast) "0")))         
         (shave-height-attr (as-number (defaulted-get-prop 'shave-height (ast-attributes photo-show-ast) "0")))         
         (resize-width-attr (as-number (defaulted-get-prop 'resize-width (ast-attributes photo-show-ast) "0")))         
         (resize-height-attr (as-number (defaulted-get-prop 'resize-height (ast-attributes photo-show-ast) "0")))         

         (landscape-percentage-attr (as-number (defaulted-get-prop 'landscape-percentage (ast-attributes photo-show-ast) default-landscape-percentage)))
         (portrait-percentage-attr (as-number (defaulted-get-prop 'portrait-percentage (ast-attributes photo-show-ast) default-portrait-percentage)))
         (photo-displacement-left-attr (as-number (defaulted-get-prop 'photo-displacement-left (ast-attributes photo-show-ast) 
                                                                      default-photo-displacement-left)))
         (photo-displacement-right-attr (as-number (defaulted-get-prop 'photo-displacement-right (ast-attributes photo-show-ast) 
                                                                      default-photo-displacement-right)))

         (camera-attr (defaulted-get-prop 'camera (ast-attributes photo-show-ast) #f))

         (image-transformation-software-attr (as-symbol (defaulted-get-prop 'image-transformation-software (ast-attributes photo-show-ast) "none")))
         (make-thumbnails-of-attr (as-symbol (defaulted-get-prop 'make-thumbnails-of (ast-attributes photo-show-ast) "none")))
         (rotate-attr (defaulted-get-prop 'rotate (ast-attributes photo-show-ast) ""))
         (time-stamp-attr (as-symbol (defaulted-get-prop 'time-stamp (ast-attributes photo-show-ast) "auto")))
         (time-stamp-presentation-attr (as-symbol (defaulted-get-prop 'time-stamp-presentation (ast-attributes photo-show-ast) "date-time")))
        )

    (set! camera-used camera-attr)
    (set! photo-displacement-left photo-displacement-left-attr)
    (set! photo-displacement-right photo-displacement-right-attr)
    (set! photo-show-verbosity-level verbosity-level-attr)
    (set! default-upper-caption 
          (if (not (blank-string? photo-show-upper-caption))
              photo-show-upper-caption
              #f))
    (set! default-lower-caption 
          (if (not (blank-string? photo-show-lower-caption))
              photo-show-lower-caption
              #f))
    (set! default-upper-size photo-show-upper-size)
    (set! default-lower-size photo-show-lower-size)
    (set! default-time time-attr)
    (set! photo-show-title title-of-photo-show)
    (set! home-path home-path-attr)
    (set! home-url (if home-path (string-append home-path "index.html") #f))
    (set! default-photo-text-color text-color-attr)
    (set! default-photo-background-color background-color-attr)
    (set! language-preference (as-symbol langauge-attr))
    (set! default-transition transition-attr)
    (set! photo-show-html-mode html-mode-attr)
    (set! photo-show-index-linking index-linking-attr)

    (set! photo-show-previous-film previous-film-attr)
    (set! photo-show-next-film next-film-attr)
    (set! photo-show-before-first before-first-attr)
    (set! photo-show-after-last after-last-attr)
    
    (set! default-photo-size default-size-attr)
    (set! default-photo-orientation default-orientation-attr)
    (set! tabular-overview-dimension tabular-overview-dimension-attr)
    (set! photo-table-window-width photo-tabular-window-width-attr)
    (set! photo-table-window-height photo-tabular-window-height-attr) 

    (set! landscape-percentage landscape-percentage-attr)
    (set! portrait-percentage portrait-percentage-attr)

    (set! photo-show-credit
          (text-choice 
           (span "LAML fotofremviseren er programmeret af Kurt Nørmark, Aalborg Universitet med brug af " 
                 (a 'href "http://www.cs.auc.dk/~normark/laml/" "LAML") "teknologi.")
 
           (span "The LAML photo show is programmed by Kurt Nørmark, Aalborg University by use of "
                 (a 'href "http://www.cs.auc.dk/~normark/laml/" "LAML") "technology.")))

    ; Make thumbnails directory - even if it is not to be used.
    (if (not (directory-exists? (string-append photo-destination-dir "thumbnails/")))
        (make-directory-in-directory photo-destination-dir "thumbnails"))

    (set! time-stamp time-stamp-attr)
    (set! time-stamp-presentation time-stamp-presentation-attr)

    (cond (photo-list-ast 
            (let* ((locally-stored-photo?
                    (lambda (photo-ast)
                      (let ((photo-path-info (ast-attribute photo-ast 'path "")))
                        (equal? photo-path-info ""))))
                   (given-photo-ast-list (traverse-and-collect-all-from-ast photo-list-ast (ast-of-type? 'element-name "photo") id-1))
                   (given-photos-in-this-directory (filter locally-stored-photo? given-photo-ast-list))
                  )
              (set! current-photo-ast photo-show-ast)
              (set! photo-list-length (length given-photo-ast-list))     
              (make-photo-show given-photo-ast-list photo-show-previous-film photo-show-next-film) 
              (internal-save-photo-ast photo-show-ast)
              (internal-save-film-description-entry film-identification photo-show-title photo-list-length)

              ; Make thumbnails creation script
              (cond ((eq? image-transformation-software-attr 'image-magick)
                       (make-imagemagick-thumbnail-script given-photos-in-this-directory tabular-overview-dimension-attr make-thumbnails-of-attr))
                    ((eq? image-transformation-software-attr 'none)
                       'nothing)
                    (else (laml-error "Unknown image transformation software:" image-transformation-software-attr)))

              ; Execute the thumbnails script
              (if (and (eq? image-transformation-software-attr 'image-magick) 
                       (not (eq? make-thumbnails-of-attr 'none)))
                  (system (string-append photo-destination-dir "internal/thumbnail")))


              ; Make shave and resized copies
              (cond ((and (eq? image-transformation-software-attr 'image-magick) shave-resize-and-copy-attr)
                       (make-imagemagick-shave-and-resize-script
                          given-photo-ast-list shave-width-attr shave-height-attr resize-width-attr resize-height-attr copy-destination-attr))
                    ((not shave-resize-and-copy-attr)
                       'nothing)
                    ((eq? image-transformation-software-attr 'none)
                       'nothing)
                    (else (laml-error "Unknown image transformation software:" image-transformation-software-attr)))

              ; Execute the shave and resize script
              (if (and (eq? image-transformation-software-attr 'image-magick) shave-resize-and-copy-attr)
                  (system (string-append photo-destination-dir "internal/shave-resize-and-copy")))


              (make-photo-overview-show given-photo-ast-list tabular-overview-dimension)
              (make-laml-fragment-file given-photo-ast-list)

              (if copy-film-attr
                  (copy-pictures! given-photo-ast-list (ensure-final-character copy-destination-attr #\/)))

            )
          )
 
          (photos-in-current-directory-ast
           (begin

             ; Make rotation creation script
             (if (not (blank-string? rotate-attr))
                 (cond ((eq? image-transformation-software-attr 'image-magick)
                        (make-imagemagick-rotation-script rotate-attr))
                       ((eq? image-transformation-software-attr 'none)
                        'nothing)
                       (else (laml-error "Unknown image transformation software:" image-transformation-software-attr))))

            ; Execute the rotation script
            (if (and (eq? image-transformation-software-attr 'image-magick)
                     (not (blank-string? rotate-attr)))
                (system (string-append photo-destination-dir "internal/rotation")))

            (let* ((derived-photo-ast-list-0 (photo-asts-in-current-directory))
                   (derived-photo-ast-list 
                     (if (and camera-used (equal? camera-used "Canon-G3"))
                         (let ((rotated-photo-names (filter rotated-photo-name? (map photo-file-name-of derived-photo-ast-list-0))))
                           (remove-redundant-landscape-photos derived-photo-ast-list-0 rotated-photo-names))
                         derived-photo-ast-list-0))
                   (modified-ast
                     (make-ast 
                      (ast-element-name photo-show-ast)
                      (list (ast-subtree photo-show-ast "upper-caption")
                            (ast-subtree photo-show-ast "lower-caption")
                            (make-ast 
                              "photo-list"
                              derived-photo-ast-list
                              '()
                              'double
                              "laml-photo"))
                      (ast-attributes photo-show-ast)
                      'double
                      "laml-photo"))
                  )
              (set! current-photo-ast modified-ast)
              (set! photo-list-length (length derived-photo-ast-list))

              (make-photo-show derived-photo-ast-list photo-show-previous-film photo-show-next-film)
              (internal-save-photo-ast modified-ast)
              (internal-save-film-description-entry film-identification photo-show-title photo-list-length)

              ; Make thumbnails creation script
              (cond ((eq? image-transformation-software-attr 'image-magick)
                       (make-imagemagick-thumbnail-script derived-photo-ast-list tabular-overview-dimension-attr make-thumbnails-of-attr))
                    ((eq? image-transformation-software-attr 'none)
                       'nothing)
                    (else (laml-error "Unknown image transformation software:" image-transformation-software-attr)))

              ; Execute the thumbnails script
              (if (and (eq? image-transformation-software-attr 'image-magick)
                       (not (eq? make-thumbnails-of-attr 'none)))
                  (system (string-append photo-destination-dir "internal/thumbnail")))


              ; Make shaved and resized copies
              (cond ((and (eq? image-transformation-software-attr 'image-magick) shave-resize-and-copy-attr)
                       (make-imagemagick-shave-and-resize-script
                          derived-photo-ast-list shave-width-attr shave-height-attr resize-width-attr resize-height-attr copy-destination-attr))
                    ((not shave-resize-and-copy-attr)
                       'nothing)
                    ((eq? image-transformation-software-attr 'none)
                       'nothing)
                    (else (laml-error "Unknown image transformation software:" image-transformation-software-attr)))

              ; Execute the shave and resize script
              (if (and (eq? image-transformation-software-attr 'image-magick) shave-resize-and-copy-attr)
                  (system (string-append photo-destination-dir "internal/shave-resize-and-copy")))


              (make-photo-overview-show derived-photo-ast-list tabular-overview-dimension)
              (make-laml-fragment-file derived-photo-ast-list)

              (if copy-film-attr
                  (copy-pictures! derived-photo-ast-list (ensure-final-character copy-destination-attr #\/)))

              (make-internal-photo-list! derived-photo-ast-list)
            ))
          )
 
          (else (laml-error "photo-show-bridging!" "Either photo-list or photos-in-current-directory must be present in a photo-show")))

    (if home-path
        (let ((home-abs-path (string-append photo-destination-dir home-path)))
          (if (safe-to-write-home-photo-page? home-abs-path)
              (make-home-photo-index)
              (display-message "** No index.html file is written in " home-abs-path ". " 
                               "Reason: index.html is in danger of being overwritten. **"))))


    (make-help-page) (make-about-page)

    (write-html (list photo-show-html-mode 'prolog) (end-page) (string-append photo-destination-dir "html/" "end-page.html"))
    (write-html (list photo-show-html-mode 'prolog) (end-page) (string-append photo-destination-dir "html/" "end-page-stop.html"))

    ; Copy process-all script to photo home dir if it is not already there.
    (let ((source-path (string-append photo-show-dir "scripts/process-all-films.laml"))
          (destination-path (string-append photo-destination-dir home-path "process-all-films.laml")))
      (if (not (file-exists? destination-path))
          (copy-file source-path destination-path)))

  )
 )
)

;; Is it considered safe to overwrite index.html in home-dir
(define (safe-to-write-home-photo-page? home-dir)
  (if (file-exists? (string-append home-dir "index.html"))
      (let ((index-html-text (read-text-file (string-append home-dir "index.html"))))
        (or 
           (substring-index index-html-text 0 "LAML Foto Hjemmeside")
           (substring-index index-html-text 0 "LAML Photo Home page")))
      #t))


; Canon G3 specific
(define (remove-redundant-landscape-photos photo-ast-list rotated-photo-names)
 (let ((downcased-rotated-photo-names (map downcase-string rotated-photo-names)))
  (filter 
   (lambda (photo-ast)
     (let* ((photo-name (photo-file-name-of photo-ast))
            (photo-name-proper (file-name-proper photo-name))
            (photo-name-ext    (file-name-extension photo-name))
          )
       (not (member (downcase-string (string-append photo-name-proper rotated-suffix-name "." photo-name-ext)) downcased-rotated-photo-names))))
   photo-ast-list)))


(define (internal-save-photo-ast ast)
 (let ((internal-dir (string-append photo-destination-dir "internal/")))
  (file-write ast (string-append internal-dir "photo-ast.lsp"))))

(define (internal-save-film-description-entry film-identification photo-show-title photo-list-length)
 (let ((internal-dir (string-append photo-destination-dir "internal/")))
  (file-write (list film-identification photo-show-title photo-list-length tabular-overview-dimension) (string-append internal-dir "film-description-entry.lsp")))) 


(define (first-url-of-photo-ast photo-ast  . optional-parameter-list)
 (let ((given-film-path (optional-parameter 1 optional-parameter-list #f))
       (suffix (optional-parameter 2 optional-parameter-list "")))
  (if photo-ast
      (let* ((photo-list-ast (ast-subtree photo-ast "photo-list"))
             (photos (if photo-list-ast (ast-subtrees photo-list-ast) '())))
        (if (not (null? photos))
            (let* ((first-photo (car photos))
                   (jpg-file-name (get-prop 'file (ast-attributes first-photo)))
                   (film-path (if given-film-path given-film-path (defaulted-get-prop 'path (ast-attributes first-photo) "")))
                   (file-name (file-name-proper jpg-file-name))
                   )
              (string-append "../" film-path "html/" file-name suffix "." "html")) 
            ""))
      "")))


(define (last-url-of-photo-ast photo-ast . optional-parameter-list)
 (let ((given-film-path (optional-parameter 1 optional-parameter-list #f))
       (suffix (optional-parameter 2 optional-parameter-list "")))
  (if photo-ast
      (let* ((photo-list-ast (ast-subtree photo-ast "photo-list"))
             (photos (if photo-list-ast (ast-subtrees photo-list-ast) '()))) 
        (if (not (null? photos))
            (let* ((last-photo (last photos))
                   (jpg-file-name (get-prop 'file (ast-attributes last-photo)))
                   (film-path (if given-film-path given-film-path (defaulted-get-prop 'path (ast-attributes last-photo) "")))
                   (file-name (file-name-proper jpg-file-name))
                   )
              (string-append "../" film-path "html/" file-name suffix "." "html")) 
            ""))
      "")))

(define (first-tabular-url-of-photo-ast photo-ast film-path)
 (if photo-ast
     (let* ((photo-list-ast (ast-subtree photo-ast "photo-list"))
            (photos (if photo-list-ast (ast-subtrees photo-list-ast) '()))
            (tab-dim (as-number (defaulted-get-prop 'tabular-overview-dimension (ast-attributes photo-ast) "3")))
           )
       (if (not (null? photos))
           (string-append "../" film-path "html/" (tabular-page-name tab-dim 1) "." "html")
           ""))
     ""))

(define (last-tabular-url-of-photo-ast photo-ast film-path)
 (if photo-ast
     (let* ((photo-list-ast (ast-subtree photo-ast "photo-list"))
             (photos (if photo-list-ast (ast-subtrees photo-list-ast) '()))  
            (photo-numbers (length photos))
            (tab-dim (as-number (defaulted-get-prop 'tabular-overview-dimension (ast-attributes photo-ast) "3")))
            (last-tab-page-number (tabular-page-count tab-dim photo-numbers))
           )
       (if (not (null? photos))
           (string-append "../" film-path "html/" (tabular-page-name tab-dim last-tab-page-number) "." "html")
           ""))
     ""))

(define (get-photo-ast path)
 (let ((ast-file-path (string-append (startup-directory) path "internal/" "photo-ast.lsp")))
   (if (file-exists? ast-file-path)
       (file-read ast-file-path)
       #f)))


; Make a size x size overview show.
(define (make-photo-overview-show photo-ast-list size)
 (let ((page-count
        (lambda (n m)
          (if (= (remainder n m) 0)
              (quotient n m)
              (+ (quotient n m) 1))))
       (overview-page-name 
         (lambda (n)
           (string-append "index" "-" (as-string size) "-" (as-string n))))
      )
   (let* ((pictures-per-page (* size size))
          (photo-lgt (length photo-ast-list))
          (photo-overview-page-count (page-count photo-lgt pictures-per-page))
          (photo-ast-list-list (sublist-by-rows pictures-per-page photo-ast-list))
          (photo-page-name-list (map overview-page-name (number-interval 1 photo-overview-page-count))))
    (map5
     (make-overview-photo-page size (+ (quotient (- (length photo-ast-list) 1) (* tabular-overview-dimension tabular-overview-dimension)) 1))
     photo-ast-list-list
     photo-page-name-list
     (if (not (null? photo-page-name-list)) (append (cdr photo-page-name-list) (list #f)) '())
     (if (not (null? photo-page-name-list)) (append (list #f) (butlast photo-page-name-list)) '())
     (number-interval 1 (length photo-page-name-list))
     ))))

(define (make-overview-photo-page size total-number)
 (let ((cur-map (curry-generalized map))
       (hs-space (horizontal-space 3))
      )
  (lambda (photo-list page-name next-page-name prev-page-name n)
   (write-html (list photo-show-html-mode 'prolog)
    (let* ((colors-list (list default-photo-background-color default-photo-text-color default-photo-text-color default-photo-text-color))
           (bg-color (first colors-list))
           (text-color (second colors-list))
           (link-color (third colors-list))
           (vlink-color (fourth colors-list))
           (down-links (map (lambda (pe) (string-append (file-name-proper (photo-file-name-of pe)) "-stop" "." "html")) photo-list))
           (last-tab-url-of-prev-film (if photo-show-previous-film (last-tabular-url-of-photo-ast (get-photo-ast photo-show-previous-film) photo-show-previous-film) ""))
           (first-tab-url-of-next-film (if photo-show-next-film (first-tabular-url-of-photo-ast (get-photo-ast photo-show-next-film) photo-show-next-film) ""))
          )
    (html 
     (head 
      (title (text-choice "LAML foto fremvisning" "LAML photo show"))
      (script "" 'src (string-append "javascript/photoNavigate.js") 'type "text/javascript")
      (link 'href "stylesheets/photo-style.css"  'rel "stylesheet" 'title "photo-style" 'type "text/css")
      (meta 'name "Generator" 'content "LAML")

     )
     (body 
      'onload (js-call "photoNavigate" 
                  (append 
                    (map string-it-single 
                          (list (if next-page-name (string-append next-page-name "." "html") first-tab-url-of-next-film)
                                (if prev-page-name (string-append prev-page-name "." "html") last-tab-url-of-prev-film)
                                ""
                                "../index.html"
                                "" "" ""
                                ))
                   (list (js-string-array down-links))
                  )
               )
      'bgcolor bg-color
      'text text-color
      'link link-color
      'vlink vlink-color

      (left-middle-right-banner 
       (con 
        (a (text-choice "Indhold" "Index") 'href "../index.html" 'class "menu-item" (tool-tip "u")) hs-space
        (cond (prev-page-name
                   (a (text-choice "Foregående" "Previous") 'href (string-append prev-page-name ".html") 'class "menu-item" (tool-tip "p")))
              ((not (empty-string? last-tab-url-of-prev-film))
                   (a (text-choice "Foregående" "Previous") 'href last-tab-url-of-prev-film 'class "menu-item" (tool-tip "p")))
              (else (text-choice "Foregående" "Previous")))
        hs-space
        (cond (next-page-name 
                 (a (text-choice "Næste" "Next") 'href (string-append next-page-name ".html") 'class "menu-item" (tool-tip "n")))
              ((not (empty-string? first-tab-url-of-next-film))
                 (a (text-choice "Næste" "Next") 'href first-tab-url-of-next-film 'class "menu-item" (tool-tip "n")))
              (else (text-choice "Næste" "Next")))
       )
 
       (span 'class "menu-item"  film-identification hs-space "-" hs-space (as-string n) ":" (as-string total-number) )

       (con
        (if home-url
            (a (text-choice "Hjem" "Home") 'href (string-append "../" home-url) 'class "menu-item")
            "") hs-space
        (a (text-choice "Om" "About") 'href (string-append (about-file-name) ".html") 'class "menu-item") hs-space
        (a (text-choice "Hjælp" "Help") 'href (string-append (help-file-name) ".html") 'class "menu-item")
        )
       )

      (center (h1 photo-show-title)) (p)

      (center
       (table 'border "0"  'cellpadding "8mm"
         (map (compose tr (cur-map (image-in-index size))) (sublist-by-rows size photo-list)))
      )        

    )
   ))
   (string-append photo-destination-dir "html/" page-name ".html")))))

(define (image-in-index size)
 (let ((percent-number (lambda (n) (string-append (as-string n) "%"))))
  (lambda (this-photo-entry)
   (let* ((this-file-name (photo-file-name-of this-photo-entry))
          (this-file-name-proper (file-name-proper this-file-name))
          (this-path (photo-path-of this-photo-entry))
          (this-upper-caption (photo-upper-caption-of this-photo-entry))
          (this-upper-size (photo-upper-size-of this-photo-entry))
          (this-lower-caption (photo-lower-caption-of this-photo-entry))
          (this-photo-orientation (photo-orientation-of this-photo-entry))

          (use-thumbnail? (file-exists? (string-append photo-destination-dir (if this-path this-path "") "thumbnails/" this-file-name)))

          (real-picture-url-prefix 
           (string-append picture-url-prefix 
                          (if this-path this-path "")))

          (real-thumb-picture-url-prefix (string-append real-picture-url-prefix "thumbnails/"))

          )
     (td (center 
          (a 'href (string-append this-file-name-proper "-stop" "." "html")
            (if use-thumbnail? 
                (if (eq? this-photo-orientation 'landscape)
                    (img 'border "0" ; 'width (as-string (quotient photo-table-window-width size))
                                     'src (string-append real-thumb-picture-url-prefix this-file-name) 'alt "")
                    (img 'border "0" ; 'height (as-string (min (quotient photo-table-window-height size) (quotient photo-table-window-width size)))
                                     'src (string-append real-thumb-picture-url-prefix this-file-name) 'alt "")
                )
                (if (eq? this-photo-orientation 'landscape)
                    (img 'border "0" 'width (as-string (quotient photo-table-window-width size)) 'src (string-append real-picture-url-prefix this-file-name) 'alt "")
                    (img 'border "0" 'height (as-string (quotient photo-table-window-height size)) 'src (string-append real-picture-url-prefix this-file-name) 'alt "")
                )
            )
          ))
         (br)
         (center (a 'href (string-append "laml-fragments.html" "#" this-file-name-proper )
                    'target "fragments"
                    (font 'size "2" this-file-name-proper) 'css:text-decoration "none"))
     )
   )
  )
 )
)


(define (make-help-page)
 (let ((lis (lambda x (li x 'css:margin-bottom "7pt")))
       (kn-url "http://www.cs.auc.dk/~normark/")
       (hs-space (horizontal-space 3))
      )
 (write-html (list photo-show-html-mode 'prolog)
    (let* ((colors-list (list default-photo-background-color default-photo-text-color default-photo-text-color default-photo-text-color))
           (bg-color (first colors-list))
           (text-color (second colors-list))
           (link-color (third colors-list))
           (vlink-color (fourth colors-list)))
    (html 
     (head 
      (title "Hjælp om LAML foto fremvisning")
      (meta 'name "Generator" 'content "LAML")

     )
     (body 

       (left-middle-right-banner 
        (a 'href "../index.html" (font-size '2 (text-choice "Indhold" "Index")) )
        ""
        (con 
          (a (text-choice "Om" "About") 'href (string-append (about-file-name) ".html") 'class "menu-item") hs-space
          (if home-url
           (a (text-choice "Hjem" "Home") 'href (string-append "../" home-url))
            "")
        )
       )

      'bgcolor bg-color
      'text text-color
      'link link-color
      'vlink vlink-color

      (h1 "Hjælp til LAML foto fremvisning")

      (div
         (p "Denne fotofremvisning er genereret af et program, som kaldes 'LAML fotogeneratoren'.
          Kort fortalt vil dette program generere en mængde HTML filer, som tillader dig at 
          se dine fotos i enhver Internet browser.")

         (p "Hjemmesiden giver et overblik over alle
             fotosider, som er processeret med LAML fotogeneratoren.")

         (p "Film indholdssiden giver et overblik over alle billeder i en enkelt 'film'. Dette inkluderer links til de enkelt billeder
             og 'n x n' links to oversigtssider med typisk 4, 9 eller 16 billeder.")

         (p "En n x n side med typisk 4, 9 eller 16 fotos viser en tabel af billeder, som er nyttigt for at danne sig en oversigt over 
             billederne i en film.")

         (p "Vi vil her beskrive, hvordan du kan navigere i dine billeder")

         (ul 
           (lis "Fra foto hjemmesiden man vælge en vilkårlig film. Tasterne 1-9 og a-m vælger en af filmene fra toppen af listen.
                 'x', 'y' og 'z' vælger de tre film fra bunden af listen.
                ")
           (lis "Fra en film indholdsside kan man direkte vælge et vilkårligt billede med musen. Man kan også vælge en 
                 n x n oversigt. Tasterne 1-9 og a-m vælger en n x n side. Tasten 'x' vælger det første billede,
                 og 'y' vælger det sidste billede.
                 Film indholdssiderne kan være kædet sammen med links, som kan navigeres med 'n' og 'p'.
                 Tasten 'u' navigerer til foto hjemmesiden.
                 ")
           (lis "Fra en n x n oversigtsside kan man vælge en af billederne for visning i fuld størrelse.
                 Man kan også navigere frem og tilbage mellem oversigtsider, samt op til film indholdssiden.
                 Dette gøres via tastaturet med hhv. 'n', 'p', og 'u'.
                 Hvis filmene er forbundet, kan man endvidere skifte til næste og foregående film fra hhv. 
                 sidste og første n x n oversigstside (igen med 'n' og 'p').
                 Hvis man vælger navnet på et billede bringes man til en side med LAML foto fragmenter, som
                 er nyttig hvis man ønsker at billedet skal indgå i et andet 'album'.")
           (lis "Fra en billedside uden automatisk skift kan man navigere til næste og foregående
                 side samt til film indholdssiden og foto hjemmesiden'. Tasterne 'n' og 'p' går frem og tilbage.
                 Tasten 'u' navigerer til den relevante n x n side. Tasten 'x' navigerer til foto indholdssiden.
                 Man kan også vælge at igangsætte automatisk skift fra
                 side til side med 'Kør', eller via tasten 'r'. 
                 Hvis man vælger navnet på et billede (i øverste linie på skærmen) bringes man til en side med LAML foto fragmenter, som
                 er nyttig hvis man ønsker at billedet skal indgå i et andet 'album'.
                 ")
           (lis "Fra en billedside med automatisk skift kan man vælge at stoppe med 'Stop' samt at gå til indholdssiden
                 eller hjemmesiden. Tasten 's' betyder 'stop'.")
         )

         (p "Følgende giver en oversigt over tasturgenveje fra billedsiderne uden automatisk skift:")
 
         (indent-pixels 10         
          (table 'border "1"
           (tr (td "Næste") (td "n, RETUR"))
           (tr (td "Foregående") (td "p"))
           (tr (td "n x n sider") (td "u"))
           (tr (td "Film indholdssiden") (td "x"))
           (tr (td "Kør") (td "r, MELLEMRUM"))
           (tr (td "Stop") (td "s, MELLEMRUM"))
           (tr (td "Kopier LAML reference til clipboard") (td "q"))
          ))

         (p "Bemærk at nogle af disse tastaturgenveje også vises, når musen hviler over et link.")

         (p "Hvis du ønsker at generere en LAML fotofremvisning kan du læse om dette på
             en separat" (a 'href "http://www.cs.auc.dk/~normark/scheme/styles/xml-in-laml/photo-show/man/photo-show.html" "manual side") _ "."
            "Hvis du ønsker at vide mere om LAML og LAML software er du velkommen til at se på" 
            (a 'href "http://www.cs.auc.dk/~normark/laml/" "LAML hjemmesiden") _ "."
         )

         (vertical-space 1)

         (p 
           "Kurt Nørmark" (br)
           (a 'href kn-url kn-url) (br)
           "normark@cs.aau.dk")
        )

    )
   ))
   (string-append photo-destination-dir "html/" "help-dk" ".html"))

   (write-html (list photo-show-html-mode 'prolog)
    (let* ((colors-list (list default-photo-background-color default-photo-text-color default-photo-text-color default-photo-text-color))
           (bg-color (first colors-list))
           (text-color (second colors-list))
           (link-color (third colors-list))
           (vlink-color (fourth colors-list)))
    (html 
     (head 
      (title "Help about LAML photo show")
      (meta 'name "Generator" 'content "LAML")

     )
     (body 

       (left-middle-right-banner 
        (a 'href "../index.html" (font-size '2 (text-choice "Indhold" "Index")) )
        ""
        (con 
          (a (text-choice "Om" "About") 'href (string-append (about-file-name) ".html") 'class "menu-item") hs-space
          (if home-url
           (a (text-choice "Hjem" "Home") 'href (string-append "../" home-url))
            "")
        )
       )

      'bgcolor bg-color
      'text text-color
      'link link-color
      'vlink vlink-color

      (h1 "Help about LAML photo shows")

      (div "In the current version the 'Help Page' is only available" (a 'href "help-dk.html" "in Danish") _ ".")

    )
   ))
   (string-append photo-destination-dir "html/" "help-en" ".html"))

))


(define (make-about-page)
 (let ((lis (lambda x (li x 'css:margin-bottom "7pt")))
       (kn-url "http://www.cs.auc.dk/~normark/")
       (hs-space (horizontal-space 3))
      )
 (write-html (list photo-show-html-mode 'prolog)
    (let* ((colors-list (list default-photo-background-color default-photo-text-color default-photo-text-color default-photo-text-color))
           (bg-color (first colors-list))
           (text-color (second colors-list))
           (link-color (third colors-list))
           (vlink-color (fourth colors-list)))
    (html 
     (head 
      (title "Om LAML fotofremvisning")
      (meta 'name "Generator" 'content "LAML")

     )
     (body 

       (left-middle-right-banner 
        (a 'href "../index.html" (font-size '2 (text-choice "Indhold" "Index")) )
        ""
        (con 
          (a (text-choice "Hjælp" "Help") 'href (string-append (help-file-name) ".html") 'class "menu-item")
          hs-space
          (if home-url (a (text-choice "Hjem" "Home") 'href (string-append "../" home-url)) "")
        )
       )

      'bgcolor bg-color
      'text text-color
      'link link-color
      'vlink vlink-color

      (h1 "Om LAML foto fremvisning")

      (div

         (p "Dette foto show er produceret af et program kaldet" (b "LAML fotogeneratoren") _ "," "som er designet og programmeret af Kurt Nørmark,
             Aalborg Universitet. Programmet genererer en række HTML filer omkring en mængde foto filer. Hermed kan fotografierne vises bekvemt i
             enhver Internet browser.")

         (p "Beskrivelsen af fremvisningens egenskaber er lavet i XML, mere præcist i et format der kaldes" 
            (a 'href "http://www.cs.auc.dk/~normark/scheme/tutorial/xml-in-laml/xml-in-laml.html" "XML-in-LAML") _ ".")

         (p (a 'href "http://www.w3.org/XML/" "XML") "er en standard for beskrivelse af strukturerede data.")

         (p (a 'href "http://www.cs.auc.dk/~normark/laml/" "LAML") "er en softwarepakke som understøtter XML, herunder HTML, i programmeringssproget" 
             (a 'href "http://www.schemers.org" "Scheme") "(et sprog i Lisp familien).
             Mere specifikt understøtter LAML muligheden for at 
             generere et" (em "spejl") "af et vilkårlig XML sprog for hvilket der findes en Document Type Definition (DTD).
             LAML har været anvendt i mange forskellige sammenhænge, primært inden for produktion af forskellige" 
             (a 'href "http://www.cs.auc.dk/~normark/leno/" "undervisningsmaterialer") "og
             " (a 'href "http://www.cs.auc.dk/~normark/elucidative-programming/index.html" "programdokumentationsværktøj") _ "." "
             LAML har været under udvikling siden 1999.")

         (p "LAML er frit software tilgængelig fra" (a 'href "http://www.cs.auc.dk/~normark/laml/" "LAML hjemmesiden") _ "."
            "LAML beror på en Scheme processor (typisk, men ikke nødvendigvis" 
            (a 'href "http://www.plt-scheme.org/" "PLT")_"'s"
            (a 'href "http://www.plt-scheme.org/software/mzscheme/" "MzScheme") "). Det anbefales, at processeringen af et LAML dokument aktiveres fra"
            (a 'href "http://www.gnu.org/software/emacs/emacs.html" "Emacs") _ "." "De fleste 
             Scheme systemer samt Emacs er ligeledes frit software." 
         )

         (p "Hvis du ønsker at generere en LAML fotofremvisning kan du læse om dette på
             en separat" (a 'href "http://www.cs.auc.dk/~normark/scheme/styles/xml-in-laml/photo-show/man/photo-show.html" "manual side") _ "."
         )

         (p "Hvis du er bruger af et sæt fotos, genereret med systemet, kan du læse om navigation mv. på" (a 'href "help-dk.html" "hjælpe siden") _ ".")

         (vertical-space 1)

         (p 
           "Kurt Nørmark" (br)
           (a 'href kn-url kn-url) (br)
           "normark@cs.aau.dk")
           
      )

    )
   ))
   (string-append photo-destination-dir "html/" "about-dk" ".html"))

   (write-html (list photo-show-html-mode 'prolog)
    (let* ((colors-list (list default-photo-background-color default-photo-text-color default-photo-text-color default-photo-text-color))
           (bg-color (first colors-list))
           (text-color (second colors-list))
           (link-color (third colors-list))
           (vlink-color (fourth colors-list)))
    (html 
     (head 
      (title "About LAML photo show")
      (meta 'name "Generator" 'content "LAML")

     )
     (body 

       (left-middle-right-banner 
        (a 'href "../index.html" (font-size '2 (text-choice "Indhold" "Index")) )
        ""
        (con 
          (a (text-choice "Hjælp" "Help") 'href (string-append (help-file-name) ".html") 'class "menu-item")
          hs-space
          (if home-url (a (text-choice "Hjem" "Home") 'href (string-append "../" home-url)) "")
        )
       )

      'bgcolor bg-color
      'text text-color
      'link link-color
      'vlink vlink-color

      (h1  "About LAML photo shows")

      (div "In the current version the 'about page' is only available" (a 'href "about-dk.html" "in Danish") _ ".")

      

    )
   ))
   (string-append photo-destination-dir "html/" "about-en" ".html"))



))



   
; Top level photo home page.
(define (make-home-photo-index)
 (let* ((film-path (lambda (foto-entry) (string-append (film-path-of foto-entry) "/" "index.html")))

        (hs-space (horizontal-space 3))
        (film-list (photo-film-list (string-append photo-destination-dir home-path)))
        (film-list-1 (filter (lambda (film-entry) (numeric-string-component (film-name-of film-entry))) film-list))
        (film-list-2 (filter (lambda (film-entry) (not (numeric-string-component (film-name-of film-entry)))) film-list))
        (sorted-film-list-1 (sort-list film-list-1 film-numeric-leq?))
        (sorted-film-list-2 (sort-list film-list-2 film-alphabetic-leq?))
        (total-film-list (append sorted-film-list-2 sorted-film-list-1))
        (film-list-lgt (length total-film-list)) 
        (reverse-sorted-film-list (reverse total-film-list))
        (first-film (if (null? total-film-list) #f (car total-film-list)))
        (first-film-path (if first-film (film-path-of first-film) #f))
        (photo-count (sum-list (map film-size-of total-film-list)))

        (down-links (map film-path total-film-list))
       )

  (write-html (list photo-show-html-mode 'prolog)
    (html
     (head (title (text-choice "LAML Foto Hjemmeside" "LAML Photo Home page"))
           (if first-film-path (script "" 'src (string-append first-film-path "/" "html/javascript/photoNavigate.js") 'type "text/javascript") '())
           (meta 'name "Generator" 'content "LAML")
;           (if first-film-path 
;               (link 'href (string-append first-film-path "/" "html/stylesheets/photo-style.css")  'rel "stylesheet" 'title "photo-style" 'type "text/css")
;               '())
     )
     (body  
      'onload (js-call "photoNavigate" 
                  (append
                    (map string-it-single 
                          (list "" "" "" ""   
                                (if (>= film-list-lgt 3) (film-path (third reverse-sorted-film-list)) "")  ; x
                                (if (>= film-list-lgt 2) (film-path (second reverse-sorted-film-list)) "") ; y
                                (if (>= film-list-lgt 1) (film-path (first reverse-sorted-film-list)) "")  ; z
                                ))
                    (list (js-string-array down-links)))
              )

      (left-middle-right-banner 
        ""
        "" 

        (con
         (a (text-choice "Om" "About") 'href (string-append first-film-path "/" "html/" (about-file-name) ".html") 'class "menu-item") hs-space
         (a (text-choice "Hjælp" "Help") 'href (string-append first-film-path "/" "html/" (help-file-name) ".html") 'class "menu-item")
        )
     )

      (photo-page-body-colors)
   
      (h1 (text-choice "Foto Hjemmeside" "Photo Page"))

      (n-column-list 3 (map photo-home-index-entry total-film-list) 1000)
;      (table 'border "0" (map photo-home-index-entry total-film-list))

      (p)
      (font-size 2 
       (text-choice (span "Ialt" (as-string photo-count) "fotos.")
                    (span "In total" (as-string photo-count) "photos.")))
      (p)
      (font-size "2" photo-show-credit) (br)
      (font 'size "2" (when-generated))
     ))
   (string-append photo-destination-dir home-path "index.html"))))


; film description entry selectors
(define film-path-of (make-selector-function 1 "film-path-of"))
(define film-name-of (make-selector-function 2 "film-name-of"))
(define film-title-of (make-selector-function 3 "film-title-of"))
(define film-size-of (make-selector-function 4 "film-size-of"))
(define film-tabular-overview-dimension (make-selector-function 5 "film-tabular-overview-dimension"))

(define (photo-home-index-entry film-entry)
  (tr 
    (td 'width "20" (horizontal-space 1))
    (td 'width "100" (a 'href (string-append (film-path-of film-entry) "/" "index.html") (capitalize-string-nd (film-name-of film-entry))))
    (td 'width "300" (film-title-of film-entry))
    (td 'width "100" (as-string (film-size-of film-entry)) (text-choice "billeder" "pictures"))
  )
)

(define (photo-home-index-entry film-entry)
 (let* ((ttl (film-title-of film-entry))
        (ttl-1 (if (empty-string? ttl) (text-choice "Ingen titel" "No title") ttl)))
   (span (a 'href (string-append (film-path-of film-entry) "/" "index.html")
            'title (capitalize-string-nd (film-name-of film-entry))
            (font 'size "2" ttl-1)) (font 'size "2" "(" _ (as-string (film-size-of film-entry)) _ ")"))))
        
(define (film-numeric-leq? fde1 fde2)
  (<= (numeric-string-component (film-name-of fde1)) (numeric-string-component (film-name-of fde2))))

(define (film-alphabetic-leq? fde1 fde2)
  (string<=? (film-name-of fde1) (film-name-of fde2)))


; Return a list of film description entries (film-name film-title)
; captured from home-dir
(define (photo-film-list home-dir)
 (let* ((subdir-list (directory-list home-dir))
        (photo-dirs (filter (lambda (subdir) (is-laml-photo-dir? home-dir subdir)) subdir-list))
        (non-photo-dirs (filter (lambda (subdir) (not (is-laml-photo-dir? home-dir subdir))) subdir-list))
       )
   (map (lambda (subdir) (cons subdir (read-film-description-entry home-dir subdir)))
        photo-dirs)))

(define (is-laml-photo-dir? path dir)
 (let ((full-path (string-append path dir "/")))
  (and 
    (file-exists? (string-append full-path "internal/" "film-description-entry.lsp"))
    (not (file-exists? (string-append full-path "exclude"))))))

(define (read-film-description-entry path dir)
  (file-read (string-append path dir "/" "internal" "/" "film-description-entry.lsp")))


; Create a HTML file with LAML photo fragment - ready to be copied.
(define (make-laml-fragment-file photo-ast-list)
   (write-html (list photo-show-html-mode 'prolog)
    (html
     (head (title (text-choice "LAML Fragmenter" "LAML Fragments"))
     )
     (body  

      (h1 (text-choice "LAML Fragmenter" "LAML Fragments"))

      (map laml-single-photo-fragment photo-ast-list)

      (vertical-space 30)
     
     ))
   (string-append photo-destination-dir "html/" "laml-fragments.html")))


(define (laml-single-photo-fragment photo-ast)
 (let* ((file-name (photo-file-name-of photo-ast))
        (proper-file-name (file-name-proper file-name)))
   (p (a 'name proper-file-name) (laml-single-photo-fragment-proper file-name)  (horizontal-space 3)
       (a 'href (string-append proper-file-name "-" "stop" "." "html") (text-choice "Billede" "Picture")))))

(define (laml-single-photo-fragment-proper file-name)
  (string-append
    "    " "(photo 'time \"3\" 'path \"../" film-identification "/\""  " 'file " (string-it file-name) " 'size \"auto\" "
    "(upper-caption 'size \"6\" \"\")" ")"))

(define (tool-tip letter)
  (if letter
      (list 'title 
            (string-append 
             (text-choice "Tastatur genvej: " "Keyboard shortcut: ")
             (as-string letter)))
      '()))

; ---------------------------------------------------------------------------------------------------

; Return first encountered number component of str.
; Return #f if no ciffer is encountered
(define (numeric-string-component str)
  (numeric-string-component-1 str 0 #f))

(define (as-natural-number ch)
  (- (as-number ch) (as-number #\0)))

(define (ciffer? ch)
  (turn-into-boolean (memv ch (list #\0 #\1 #\2 #\3 #\4 #\5 #\6 #\7 #\8 #\9 ))))

(define (numeric-string-component-1 str i res)
  (cond ((>= i (string-length str)) res)
        ((and (not res) (ciffer? (string-ref str i)))  (numeric-string-component-1 str (+ i 1) (as-natural-number (string-ref str i))))
        ((and res (ciffer? (string-ref str i)))  (numeric-string-component-1 str (+ i 1) (+ (* 10 res) (as-natural-number (string-ref str i)))))
        ((and res (not (ciffer? (string-ref str i)))) res)
        ((and (not res) (not (ciffer? (string-ref str i)))) (numeric-string-component-1 str (+ i 1) #f))
        (else (laml-error numeric-string-component str) "Internal error. Should not happen.")))
            
; --------------------------------------------------------------------------------------------------- 
; Photo copy support.

(define (copy-pictures! photo-ast-list absolute-path)
  (for-each
    (lambda (photo-ast)
      (let* ((photo-path (photo-path-of photo-ast))
             (photo-name (photo-file-name-of photo-ast))
             (source-path (string-append photo-destination-dir (if photo-path photo-path "") photo-name))
             (destination-path (string-append absolute-path (ensure-non-existing-file-in-dir photo-name absolute-path)))  ; (string-append absolute-path photo-name) 
            )
       (if (not (file-exists? destination-path))
           (begin
             (copy-file source-path destination-path)
             (if (>= photo-show-verbosity-level 1) 
                 (display-message "Copying" photo-name "to" destination-path))
           )
           (display-message "Destination file exists" destination-path "- NO copy made."))))
    photo-ast-list))


; ---------------------------------------------------------------------------------------------------
; Making internal photo-list -ready to put into LAML source.

(define (make-internal-photo-list! photo-ast-list)
  (write-text-file
    (string-append
      "  " "(photo-list" CR

      (accumulate-right string-append ""
        (map make-photo-clause-string photo-ast-list))

      CR "  " ")"
    )

    (string-append photo-destination-dir "/" "internal" "/" "photo-list")
  )
)

(define (make-photo-clause-string photo-ast)
 (let ((tm (photo-show-time-of photo-ast))
       (pth (photo-path-of photo-ast))
       (fl (photo-file-name-of photo-ast))
       (sz (photo-size-of photo-ast))
       (ori (photo-orientation-of photo-ast))
       (ls (photo-lower-size-of photo-ast))
       (us (photo-upper-size-of photo-ast))
       (uc (photo-upper-caption-of photo-ast))
       (lc (photo-lower-caption-of photo-ast))
      )

  (string-append
     "    " "(photo "
     (if tm (string-append "'time " (string-it (as-string tm)) " ") "")
     (if pth (string-append "'path " (string-it (as-string pth)) " ") "")
     (if fl (string-append "'file " (string-it (as-string fl)) " ") "")
     (if sz (string-append "'size " (string-it (as-string sz)) " ") "")
     (if ori (string-append "'orientation " (string-it (as-string ori)) " ") "")
     (if uc (string-append "(upper-caption " (if us (string-append "'size " (string-it (as-string us))) "") " " (string-it uc) ")") "") 
     (if lc (string-append "(lower-caption " (if ls (string-append "'size " (string-it (as-string ls))) "") " " (string-it lc) ")") "")
     ")" CR
  )
 )
)

; --------------------------------------------------------------------------------------------------- 
  
; thumbnails-mode corresponds to the photo-show attribute make-thumbnails-of.
(define (make-imagemagick-thumbnail-script photo-list-ast tabular-overview-dimension thumbnails-mode)
 (let ((windows-path (string-append photo-destination-dir "internal/" "thumbnail.bat"))
       (unix-path    (string-append photo-destination-dir "internal/" "thumbnail"))
       (thumbnail-script-list
         (filter string? (map (lambda (photo-ast) (make-imagemagick-thumbnail-entry photo-ast tabular-overview-dimension thumbnails-mode)) photo-list-ast)))
      )
  (if (> (length thumbnail-script-list) 0)
      (if (>= photo-show-verbosity-level 1) (display-message "Making" (length thumbnail-script-list) "thumbnail photos."))
      (if (>= photo-show-verbosity-level 1) (display-message "No tumbnails are being made")))

  (write-text-file
    (string-append
      "@echo off" CR
      "rem ImageMagic script for production of thumbnails" CR
      "rem cf. www.ImageMagick.org" CR
      
      "rem  By Kurt Normark, normark@cs.aau.dk" CR
      (list-to-string   ; disregard boolean #f entries
        thumbnail-script-list
        CR)
      CR 
    )
    windows-path)

  (write-text-file
    (string-append
      "#!/bin/sh " CR
      "# By Kurt Normark, normark@cs.aau.dk" CR
      
      (list-to-string 
        thumbnail-script-list
        CR)
      CR 
    )
    unix-path)

  (if (eq? laml-platform 'unix)
      (system (string-append "chmod 755 " unix-path)))

))

(define (make-imagemagick-shave-and-resize-script photo-list-ast shave-width-attr shave-height-attr resize-width-attr resize-height-attr copy-destination-dir)
 (let ((windows-path (string-append photo-destination-dir "internal/" "shave-resize-and-copy.bat"))
       (unix-path    (string-append photo-destination-dir "internal/" "shave-resize-and-copy"))
       (shave-resize-and-script-list
         (filter string? (map (lambda (photo-ast)
                                 (make-shave-resize-and-copy-entry 
                                    photo-ast shave-width-attr shave-height-attr resize-width-attr resize-height-attr copy-destination-dir))
                               photo-list-ast)))
      )
  (if (> (length shave-resize-and-script-list) 0)
      (if (>= photo-show-verbosity-level 1) (display-message "Shaving, resizing and copying" (length shave-resize-and-script-list) "photos."))
      (if (>= photo-show-verbosity-level 1) (display-message "No shaving, resizing and copying is done")))

  (write-text-file
    (string-append
      "@echo off" CR
      "rem ImageMagic script for production of thumbnails" CR
      "rem cf. www.ImageMagick.org" CR
      
      "rem  By Kurt Normark, normark@cs.aau.dk" CR
      (list-to-string   ; disregard boolean #f entries
        shave-resize-and-script-list
        CR)
      CR 
    )
    windows-path)

  (write-text-file
    (string-append
      "#!/bin/sh " CR
      "# By Kurt Normark, normark@cs.aau.dk" CR
      
      (list-to-string 
        shave-resize-and-script-list
        CR)
      CR 
    )
    unix-path)

  (if (eq? laml-platform 'unix)
      (system (string-append "chmod 755 " unix-path)))

))



(define (make-imagemagick-rotation-script rotation-description)
 (let* ((windows-path (string-append photo-destination-dir "internal/" "rotation.bat")) 
        (unix-path (string-append photo-destination-dir "internal/" "rotation")) 
        (rotation-desc-list-1 (parse-rotation-description rotation-description))
        (rotation-desc-list-2
         (filter
          (lambda (rot-desc)
            (and (file-exists? (string-append photo-destination-dir (car rot-desc)))
                 (not (file-exists? (string-append photo-destination-dir 
                                                   (file-name-proper (car rot-desc)) rotated-suffix-name "." (file-name-extension (car rot-desc)))))))
          rotation-desc-list-1))
        (thumbnail-script-list
         (map (lambda (rot-desc) (make-imagemagick-rotation-entry rot-desc)) rotation-desc-list-2))
        )
   (if (> (length rotation-desc-list-2) 0)
       (if (>= photo-show-verbosity-level 1) (display-message "Rotating " (length rotation-desc-list-2) " photos."))
       (if (>= photo-show-verbosity-level 1) (display-message "No rotations done")))

   (write-text-file
    (string-append
     "@echo off" CR
     "rem ImageMagic script for rotation of photos" CR
     "rem cf. www.ImageMagick.org" CR
      
     "rem  By Kurt Normark, normark@cs.aau.dk" CR
     (list-to-string                    ; disregard boolean #f entries
      thumbnail-script-list
      CR)
     CR 
     )
    windows-path)

   (write-text-file
    (string-append
     "#!/bin/sh " CR
     "# By Kurt Normark, normark@cs.aau.dk" CR

     (list-to-string                   
      thumbnail-script-list
      CR)
     CR 
     )
    unix-path)

   (if (eq? laml-platform 'unix)
       (system (string-append "chmod 755 " unix-path)))

))

(define (make-shave-resize-and-copy-entry photo-ast shave-width-attr shave-height-attr resize-width-attr resize-height-attr copy-destination-dir)
 (let* ((file-name (photo-file-name-of photo-ast))
        (contribution
          (string-append
           "convert" " "
           photo-destination-dir file-name " "
           "-shave" " " (as-string shave-width-attr) "x" (as-string shave-height-attr) " "
           "-resize" " "  (as-string resize-width-attr) "x" (as-string resize-height-attr) "!" " "
           copy-destination-dir file-name))
       )
  contribution))

; December 23, 2007:
(define (make-shave-resize-and-copy-entry photo-ast shave-width-attr shave-height-attr resize-width-attr resize-height-attr copy-destination-dir)
 (let* ((file-name (photo-file-name-of photo-ast))
        (contribution
          (string-append
           "convert" " "
           photo-destination-dir 
           (photo-path-of photo-ast)   ; added
           file-name " "
           "-shave" " " (as-string shave-width-attr) "x" (as-string shave-height-attr) " "
           "-resize" " "  (as-string resize-width-attr) "x" (as-string resize-height-attr) "!" " "
           copy-destination-dir file-name))
       )
  contribution))

; rot-desc is a cons-pair of the form (file . rot-number)
(define (make-imagemagick-rotation-entry rot-desc)
 (let* ((file-name (car rot-desc)) 
        (degr (as-string (cdr rot-desc)))
        (contribution
          (string-append
           "convert" " "
           photo-destination-dir file-name " "
           "-rotate" " " degr " "
           photo-destination-dir (file-name-proper file-name) rotated-suffix-name "." (file-name-extension file-name)))
       )
  contribution))


(define (parse-rotation-description rotation-description)
  (let* ((rotation-list (split-string-by-predicate rotation-description (lambda (ch) (memv ch white-space-char-list))))
         (rotation-list-1 
           (map 
             (lambda (rotation-desc) 
               (let ((p1 (find-in-string-from-end rotation-desc #\:)))
                 (if p1
                     (cons
                       (substring rotation-desc 0 p1)
                       (as-number (substring rotation-desc (+ p1 1) (string-length rotation-desc))))
                     (laml-error "Unknown format of rotation-desription: " rotation-desc))))
             rotation-list)))
   rotation-list-1))


(define (make-imagemagick-thumbnail-entry photo-ast tabular-overview-dimension thumbnails-mode)
 (let* ((file-name (photo-file-name-of photo-ast))
        (path (photo-path-of photo-ast))
        (thumb-path (string-append photo-destination-dir (if path path "") "thumbnails/" file-name))
        (size (quotient photo-table-window-width tabular-overview-dimension)) 
        (size-string (string-append (as-string size) "x" (as-string size)))
        (contribution
          (string-append
           "convert" " "
           "-size" " " size-string " "
           photo-destination-dir file-name " "
           "-resize" " " size-string " "
           "+profile" " " "\"*\"" " "
           photo-destination-dir "thumbnails/" file-name))
      )
  (cond ((eq? thumbnails-mode 'none) #f)
        ((eq? thumbnails-mode 'non-existing) 
            (if (file-exists? thumb-path) #f contribution))
        ((eq? thumbnails-mode 'all) contribution)
        (else (laml-error "make-imagemagick-thumbnail-entry: Unknown thumbnails-mode:" thumbnails-mode)))))

; ---------------------------------------------------------------------------------------------------
; Time stamps

; Return the time stamp of file (identified by the full path). A second count.
; Return #f if it is not possible to deliver time.
; The globale variable time-stamp tells how to get the time stamp.
(define (get-jpg-file-time-stamp full-photo-path)
 (cond ((eq? time-stamp 'auto)
          (let ((t1 (access-time-in-jpg-file full-photo-path))
                (t2 (if (eq? scheme-system 'mzscheme-200)
                        (file-or-directory-modify-seconds full-photo-path)
                        #f)))
            (cond (t1 t1)
                  (t2 t2)
                  (else #f)))) 
       ((eq? time-stamp 'from-file-system)
           (if (eq? scheme-system 'mzscheme-200)
               (file-or-directory-modify-seconds full-photo-path)
               #f))
       ((eq? time-stamp 'from-jpg-file)
           (access-time-in-jpg-file full-photo-path))
       ((eq? time-stamp 'none)
           #f)
       (else (laml-error "get-jpg-file-time-stamp: Unknown time-stamp mode:" time-stamp))))    


(define prefix-size 400)

; Access the jpg file and attemp to access time info, as recorded by the camera.
; Look for a certain pattern in the the first part of the file.
; Return a second count, or #f in case of failure.
(define (access-time-in-jpg-file full-photo-path)
  (let* ((prefix-list (prefix-file-part full-photo-path prefix-size))
         (prefix-vector (list->vector prefix-list))
         (time-start-index (look-for-time-pattern prefix-vector 0)))  ; index of year
   (if time-start-index
       (let ((year (char-list-to-string (vector-portion-to-list prefix-vector time-start-index 4)))
             (month (char-list-to-string (vector-portion-to-list prefix-vector (+ time-start-index 5) 2)))
             (day (char-list-to-string (vector-portion-to-list prefix-vector (+ time-start-index 8) 2)))
             (hour (char-list-to-string (vector-portion-to-list prefix-vector (+ time-start-index 11) 2)))
             (minute (char-list-to-string (vector-portion-to-list prefix-vector (+ time-start-index 14) 2)))
             (second (char-list-to-string (vector-portion-to-list prefix-vector (+ time-start-index 17) 2)))
            )
         (time-encode
           (as-number year) (as-number month) (as-number day) (as-number hour) (as-number minute) (as-number second))
       )
       #f)))

(define ciffer-list (list #\0 #\1 #\2 #\3 #\4 #\5 #\6 #\7 #\8 #\9 ))

; Look in the jpg-vector for a pattern that matches the time in a JPG file. Example 2005:04:22 15:05:11
; Return the position in the vector, where the time pattern is located. 
; Return #f in case it is impossible to locate a time pattern.
(define (look-for-time-pattern jpg-vector start)
  (let ((pos (find-in-vector-by-predicate (lambda (el) (eqv? el #\:)) jpg-vector start)))  ; look for first ':'
    (if pos
        (if (and (memv (vector-ref jpg-vector (- pos 4)) ciffer-list) ; year
                 (memv (vector-ref jpg-vector (- pos 3)) ciffer-list)
                 (memv (vector-ref jpg-vector (- pos 2)) ciffer-list)
                 (memv (vector-ref jpg-vector (- pos 1)) ciffer-list)
                 (memv (vector-ref jpg-vector (+ pos 1)) ciffer-list) ; month
                 (memv (vector-ref jpg-vector (+ pos 2)) ciffer-list)
                 (memv (vector-ref jpg-vector (+ pos 3)) (list #\:))
                 (memv (vector-ref jpg-vector (+ pos 4)) ciffer-list) ; day
                 (memv (vector-ref jpg-vector (+ pos 5)) ciffer-list) ; ... enough ...
             )
             (- pos 4)
             (look-for-time-pattern jpg-vector (+ pos 1)))
        #f)))

; To general.
(define (find-in-vector-by-predicate pred vec . optional-parameter-list)
  (let ((start-pos (optional-parameter 1 optional-parameter-list 0)))
    (find-in-vector-by-predicate-1 pred vec start-pos start-pos (vector-length vec))))

(define (find-in-vector-by-predicate-1 pred vec start-pos i lgt)
  (cond ((and (boolean? start-pos) (not start-pos)) #f)
        ((>= i lgt) #f)
        ((pred (vector-ref vec i)) i)
        (else (find-in-vector-by-predicate-1 pred vec start-pos (+ i 1) lgt))))



; Return lgt elements of vector starting at position start.
; Assume, as a precondition, that there are sufficient elements.
(define (vector-portion-to-list vector start lgt)
  (if (= lgt 0)
      '()
      (cons (vector-ref vector start)
            (vector-portion-to-list vector (+ start 1) (- lgt 1)))))
            
  
    

;; Make and return a string from the chararcters in char-list.
(define (char-list-to-string char-list)
  (let* ((nchars (length char-list))
         (str (make-string nchars))
         (index-list (number-interval 0 (- nchars 1))))
    (for-each
      (lambda (ch i)
        (string-set! str i ch))
      char-list index-list)
    str))



(define (prefix-file-part file-name n)
 (let* ((ip (open-input-file file-name))
        (res (read-n-characters ip n 0)))
     (close-input-port ip)
     res))

(define (read-n-characters ip n i)
 (cond ((= i n) '())
       ((< i n) (cons (read-char ip) (read-n-characters ip n (+ i 1))))))
  

