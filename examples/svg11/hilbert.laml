(load (string-append laml-dir "laml.scm"))
(laml-style "simple-svg11")

(load (in-startup-directory "svg-paths.scm"))

(begin-laml)

(define xml-validate-contents? #t)
(define xml-check-attributes? #t)
(define xml-transliterate-character-data? #t)

(define unit-length 1000)

(define (hilbert-height n)
 (if (= n 0)
     0
     (+ 1 (* 2 (hilbert-height (- n 1))))))

(define (up-line el) (rv-p (- el) (e-p)))
(define (down-line el) (rv-p el (e-p)))
(define (left-line el) (rh-p (- el) (e-p)))
(define (right-line el) (rh-p el (e-p)))

(define (hilbert n turn)
 (let ((edge-length (quotient unit-length (hilbert-height n))))
   (hilbert-1 n turn edge-length)))

(define (hilbert-1 n turn el)
 (cond ((= n 0)
          (e-p))
       ((> n 0)
         (cond 
             ((eq? turn 'up) 
               (concat-p
                 (hilbert-1 (- n 1) 'right el)  
                 (up-line el)
                 (hilbert-1 (- n 1) 'up el)  
                 (right-line el)
                 (hilbert-1 (- n 1) 'up el)  
                 (down-line el)
                 (hilbert-1 (- n 1) 'left el)  
                )
             )

             ((eq? turn 'left) 
               (concat-p
                 (hilbert-1 (- n 1) 'down el)  
                 (left-line el)
                 (hilbert-1 (- n 1) 'left el)  
                 (down-line el)
                 (hilbert-1 (- n 1) 'left el)  
                 (right-line el)
                 (hilbert-1 (- n 1) 'up el)  
                )
             )

             ((eq? turn 'right)
               (concat-p
                 (hilbert-1 (- n 1) 'up el)  
                 (right-line el)
                 (hilbert-1 (- n 1) 'right el)  
                 (up-line el)
                 (hilbert-1 (- n 1) 'right el)  
                 (left-line el)
                 (hilbert-1 (- n 1) 'down el)  
                )
             )
             ((eq? turn 'down)
               (concat-p
                 (hilbert-1 (- n 1) 'left el)  
                 (down-line el)
                 (hilbert-1 (- n 1) 'down el)  
                 (left-line el)
                 (hilbert-1 (- n 1) 'down el)  
                 (up-line el)
                 (hilbert-1 (- n 1) 'right el)  
                )
             )
          )
        )
  )
)


(write-html 'raw

  (svg 'width "800" 'height "800" 'viewBox (box 0 0 (+ 10 unit-length) (+ 10 unit-length))
   (g 'stroke "black" 'fill "white" 'stroke-width "1"
    (path 'd (am-p 5 (- unit-length -5) (hilbert 6 'up) )
       )))


  (full-source-path-with-extension "svg")
)

(end-laml)







